ZVSE2

You can set the following modifiers:

SN:W^Arena_%Y1_Regeneration_Value^/0; Regeneration in percent of HP per turn
SN:W^Arena_%Y1_Cast_Spells^/0; Number of casts on target before any attack
SN:W^Arena_%Y1_Cast_Spell_ID^/0; Spell ID that should be cast
SN:W^Arena_%Y1_Cast_Spell_Damage^/0; Set the damage the spell should cause
SN:W^Arena_%Y1_Extra_Shots^/0; Number of extra strikes
SN:W^Arena_%Y1_Extra_Melee^/0; Number of extra shots
SN:W^Arena_%Y1_Additional_Melee^/0; Number of additional strikes on a random target
SN:W^Arena_%Y1_Random_Shots^/0; Number of additional shots on a random target
SN:W^Arena_%Y1_Act_Twice^/0; Number of actings per round
SN:W^Arena_%Y1_AOE_Damage_Value^/0;   [set percent as AOE damage]
SN:W^Arena_%Y1_Resurrect_Value^/0; set the amount of HP a creature resurrects with in total HP
SN:W^Arena_%Y1_Resurrect_Times^/0; set the number of times a creature can resurrect
SN:W^Arena_%Y1_Dodge_Value^/0; set the chance to dodge any most physical attacks
SN:W^Arena_%Y1_Summon_Creature_ID^/0; set the creature ID that should be summoned
SN:W^Arena_%Y1_Summon_Creature_Numbers^/0; set the amount of creatures that will be summoned
SN:W^Arena_%Y1_Block_Chance^/0; set the chance by which the stack will block
SN:W^Arena_%Y1_Block_Value^/0; set the amount of defelect in percent from 1 to 100
SN:W^Arena_%Y1_BuffSpell_ID_1^/0; set spell 1 ID which should be applied at battle start on stack 21
SN:W^Arena_%Y1_BuffSpell_Power_1^/; set spell power/effect of that spell, can have different meaning
SN:W^Arena_%Y1_BuffSpell_ID_2^/0; set spell 2 ID which should be applied at battle start on stack 21
SN:W^Arena_%Y1_BuffSpell_Power_2^/; set spell power/effect of that spell, can have different meaning



****************************************************************************************************




                                  Combat Scripting of Boss Battles




****************************************************************************************************
*code to set new creature name and description for a stack. Values are defined in the arena template
!?FU(OnBattleScreenMouseClick)&i^mouse_battleHex^>0/i^mouse_battleHex^<186/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]
!!CM:D?y1;                              [get click position]
!!BU:Ey1/?y2;                           [check if a monster is at that place]
!!FU&y2=-1:E;                           [get monster ID at position and exit if no monster]
!!if&y2>=21;                            [if clicked on stack 21]
  !!SN:W^Arena_Challenge_Number^/?y50;
  !!if&y50>=16/y50<=30;                 [Only set description for boss battles]
    !!VRz1:Ss^Arena_%Y50_Boss_Description^;
    !!BMy2:Z?(stackStruct:y);           [Get the stack struct]
    !!FU(WOG_AllocBattleStr):Pz1/?(newDescAddr:y); [get string address]
    !!UN:C(stackStruct)/144/(UNC_INT)/(newDescAddr); [set new description]
  !!en;
!!en;


****************************************************************************************************
*Code to set stats of Arena Bosses
!?FU(OnAfterTacticsPhase)&i^Arena_Fight^;[Moved timing from OnCombatRound to OnAfterTacticsPhase to have correct move order in battle]
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!VRy30:Si^Arena_%Y1_Boss_Attack_Scaled^;      [Get Boss stats]
!!VRy31:Si^Arena_%Y1_Boss_Defense_Scaled^;   
!!VRy32:Si^Arena_%Y1_Boss_Shots^;   
!!VRy33:Si^Arena_%Y1_Boss_Damage_Scaled^;   
!!VRy34:Si^Arena_%Y1_Boss_Health_Scaled^;   
!!VRy35:Si^Arena_%Y1_Boss_Speed_Scaled^;
!!VRz10:Ss^Arena_%Y1_Boss_Name^;   
!!FU|y30=0/y31=0/y33=0/y34=0/y35=0:E;   [Exit if no valid stats have been set]



!!re i/21/41;
  !!BMi:Ay30 Dy31 U1/y33 U2/y33 U3/y32 Hy34 Sy35 F?y40 F?y41 T?y42; [Set Parameters of the creature]
  !!VRy41:|65536;                       [set no enemy retaliation flag]
  !!BMi:Fy41;                           [set no enemy retaliation flag]
!!en;


*All bosses are imune to fear. Code by Archer
!?FU(ACM_CreateERMHook);
!#VA(setHook:x);
!!SN:E(setHook)/1/4606276/(ACM_OnCheckFear); 464944

!?FU(ACM_OnCheckFear)&i^Arena_Fight^;
!#VA(hook:x);
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EDI)/4/?(stackStruct:y);
!!UN:C(stackStruct)/244/4/?(side:y) C(stackStruct)/248/4/?(stackOfSide:y);
!!VR(stackId:y):S(side) *(BATTLE_STACKS_PER_SIDE) +(stackOfSide);

; Add your condition
!!if&(stackId)>=21;
  ......
  ; Skip Fear
  !!SN:X?t/0;
  !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_RET)/4/4606265;
!!en;

****************************************************************************************************


Some scripts that are enabled in all Arena Boss battle fights


****************************************************************************************************

*The physical damage a Arena boss can receive is never more than 20% of his max HP with one strike for physical damage
!?MF1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!UN:C42149568/4/?y10;                  [Physical Damage]
!!if|y10=4462398/y10=4455011/y10=4456676/y10=4454752;[Only run when melee or shooting damage]
  !!BG:A?y3;
    !!if|y3=6/y3=7;                     [If Melee or Range]
    !!MF:N?y4;                          [damage receiving stack]
    !!BG:N?y5;
    !!BMy4:I?y7 H?y6;                   [Monster Side Index and max HP]
    !!if&y4=21;                         [if any defender stack and defender side]
      !!MF:F?y9;                        [get damage]
      !!VRy6:*20:100;                   [get 20% of max HP of Raid Boss]
      !!MF&y9>y6:Fy6;                   [Set damage to max 20%]
    !!en;
  !!en;
!!en;


*The magic damage a Arena boss can receive is never more than 25% of his max HP with one cast for magical damage
!?MR1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]
  !!BG:A?y3;
    !!if&y3=1;                          [If hero cast]
    !!MR:N?y4;                          [damage receiving stack]
    !!BG:N?y5;
    !!BMy4:I?y7 H?y6;                   [Monster Side Index and max HP]
    !!if&y4=21;                         [if any defender stack and defender side]
      !!MR:F?y9;                        [get damage]
      !!VRy6:*25:100;                   [get 25% of max HP of Raid Boss]
      !!MR&y9>y6:Fy6;                   [Set magic damage]
    !!en;
  !!en;


*Raid Boss has a chance to deal double damage against single untis.
This is done to reduce the chance of strong single units tanking the monster because of heal.
!?FU(OnStackToStackDamage)&i^Arena_Fight^;
*!FU:E;                                 [disabled because damage should usually be enough to kill single targets anyway]

!!FU&x9=1:E;                            [Exit calculation for theoretical attack]
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!if&x1=21;                             [if any valid battlestack]
  !!if&x2>=0/x2<=20;                    [if any valid battlestack]
    !!BMx2:N?y2;                        [get number of units]    
    !!VRx4&y2=1:*3:2;                   [with 100% chance to deal +50% damage]
  !!en;
!!en;


*The raid boss is immune vs Death Stare attack
!?MF1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!UN:C42149568/4/?y10;                  [DeathStare Damage]
!!if&y10=4460149;                       [Only run when death gaze damage]
  !!BG:A?y3;
    !!if|y3=6/y3=7;                     [If Melee or Range]
    !!MF:N?y4;                          [damage receiving stack]
    !!BG:N?y5;
    !!BMy4:I?y7 H?y6;                   [Monster Side Index and max HP]
    !!if&y4=21;                         [if any defender stack and defender side]
      !!MF:F0;                          [set damage to zero]
    !!en;
  !!en;
!!en;


*Arena Boss monsters loose all debuffs at end of every combat round
Also if speed is below 4 at a new combat round it will reset
Also after 8 combat round stats begin to increase slowly
!?FU(OnCombatRound)&i^Arena_Fight^/v997>=1;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!re i/21/41;
    !!FU(AC_ResetSpellFromStack):Pi/42; [42 Curse]
    !!FU(AC_ResetSpellFromStack):Pi/52; [52 Misfortune]
    !!FU(AC_ResetSpellFromStack):Pi/45; [45 Weakness]
    !!FU(AC_ResetSpellFromStack):Pi/54; [54 Slow]
    !!FU(AC_ResetSpellFromStack):Pi/50; [50 Sorrow]
    !!FU(AC_ResetSpellFromStack):Pi/52; [52 Misfortune]
    !!FU(AC_ResetSpellFromStack):Pi/62; [62 Blind]
    !!FU(AC_ResetSpellFromStack):Pi/59; [59 Berserk]
    !!FU(AC_ResetSpellFromStack):Pi/60; [60 Hypnotize]
    !!FU(AC_ResetSpellFromStack):Pi/61; [61 Forgetfulness]
    *!FU(AC_ResetSpellFromStack):Pi/70;[Stone]
    *!FU(AC_ResetSpellFromStack):Pi/71;[Poison]
    !!FU(AC_ResetSpellFromStack):Pi/72; [Bind]
    *!FU(AC_ResetSpellFromStack):Pi/73;[Disease]
    !!FU(AC_ResetSpellFromStack):Pi/74; [Paralyze]
    *!FU(AC_ResetSpellFromStack):Pi/75;[Age]
!!en;

!!BM21:S?y5 H?y6;                       [Get Creature Speed and HP]
!!BM21&y5<4:S4;                         [reset speed to 4 if it is to low]
!!BM21&v997>=8:Sd1 Ad2 Dd2 U2/d25;      [slowly increase stats after round 8]


*Arena Boss monsters have a 25% Magic Resistance Dwarv Type
!?MR2&i^Arena_Fight^; !!FU:E; because handled elsewhere
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!MR:N?y1;                              [Get Creature Number]
!!BMy1:I?y2 T?y3;                       [Get Creature Side]
!!if&y1>=21/y1<=41;                     [if an enemy stack]
  !!MR:F?y40;
  !!VRy40:+25;                          [Add MR from com class to current MR value]
  !!VRy40&y40>100:S100;
  !!MR:Fy40;                            [Apply increased resistance chance to that stack temporarily]
!!en;

*Arena Boss monsters have a 25% Magic Resistance Golem Type
!?MR1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]
!!MR:N?y5 S?y30;                        [Get Creature number which took magic damage]
!!if&y5>=21/y5<=41;                     [if an enemy stack]
  !!MR:F?y10; !!FU&y10=0:E;             [Final (corrected) damage]
  !!VRy11:Sy10*75:100;                  [make 25% damage reduction]
  !!MR&y11>0:Fy11;                      [Set to damage without any reduction]
!!en;


*all bosses have a 20% damage reduction, because why not
!?FU(OnStackToStackDamage)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!if&x1>=0/x1<=20;                      [if any valid attacker battlestack]
  !!if&x2>=21/x2<=41;                   [if any valid battlestack]
    !!VRx4:*80:100;                     [20% damage reduction]
    !!VRx4&x4<0:S0;                     []
  !!en;
!!en;


*damage increase and reduction. All bosses will deal +1000 damage and absorb 500 flat damage
!?FU(OnStackToStackDamage)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]

!!if&x1>=0/x1<=20;                      [if any valid attacker battlestack]
  !!if&x2>=21/x2<=41;                   [if any valid battlestack]
    !!VRx4:-500;                        [-500 flat damage reduction]
    !!VRx4&x4<0:S0;                     []
  !!en;
!!en;

!!if&x1>=21/x1<=41;                     [if any valid defender battlestack]
  !!if&x2>=0/x2<=20;                    [if any valid battlestack]
    !!VRx4:+1000;                       [+1000 damage]
  !!en;
!!en;


*Each Raid Boss has a very low chance to Blind,Root,Paralyze,StoneGaze with attacks
tbd if I keep it
!?FU(ACM_OnAfterAttack)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU|y1<16/y1>30:E;                     [Exit of no boss battle arena ID]
!!if&x1=21;                             [If Raid Boss]
  !!VRy3:S2;                            [Set 2% chance]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M70/3/3; [Stone]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M71/3/3; [Poison]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M72/3/3; [Bind]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M73/3/3; [Disease]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M74/3/3; [Paralyze]
  !!VRy1:S0R100; !!BMx2&y1<=y3:M75/3/3; [Age]
!!en;
****************************************************************************************************




                  Universal scripts that might be used later





****************************************************************************************************
*damage reduction and spectral hit enchantment
!?FU(OnStackToStackDamage)&i^Arena_Fight^;
!!FU&x9=1:E;                            [Exit calculation for theoretical attack]
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!FU:E;                                 [disabled for now]

!!if&x1>=0/x1<=20;                      [if any valid battlestack]
  !!if&x2>=21/x2<=41;                   [if any valid battlestack]
    !!VRx4&y1=1/y2=0:*70:100;           [30% damage reduction]
  !!en;
!!en;

!!VRy22:Si^timerMonth^+1;               [Put Current Week in y22]
!!VRy23&y1=1/y2=0:Sy22*100;             [Add 100 extra damage per game month with attacks]

!!if&x1>=21/x1<=41;                     [if any valid battlestack]
  !!if&x2>=0/x2<=20;                    [if any valid battlestack]
    !!VRx4&y1=1/y2=0:+y23;              [+100 damage per month with attacks]
  !!en;
!!en;


*Casting before attack
!?FU(OnBeforeBattleAction)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]

!!SN:W^Arena_%Y1_Cast_Spells^/?y5; !!FU&y5=0:E;
!!SN:W^Arena_%Y1_Cast_Spell_ID^/?y20; !!FU&y20=0:E;

!!BG:A?y7 E?y2 N?y3 D?y4;               [Aktion in y1]
!!if|y7=6/y7=7;
!!if&y2>=0/y2<=41/y3>=21/y4>-1;         [if all conditions are true]
  !!re i/1/y5;
    !!VRy9:S1R8;                        [choose random spell]
    !!VRy10&y9=1:S15;                   [spell magic arrow]
    !!VRy10&y9=2:S16;
    !!VRy10&y9=3:S17;
    !!VRy10&y9=4:S18;
    !!VRy10&y9=5:S19;
    !!VRy10&y9=6:S20;
    !!VRy10&y9=7:S21;
    !!VRy10&y9=8:S22;
    !!VRy10&y9=9:S23;
    !!VRy10&y20>0:Sy20;
    !!SN:W^Arena_%Y1_Cast_Spell_Damage^/?y8; [Set the damage the spell should cause]
    !!if&y8>0;                          [If damage value is set]
      !!SSy10:E3/?y21 P?y22;            [Save spell effect for expert and power]
      !!SSy10:E3/y8 P0;                 [Set temporarily to zero]
    !!en;
    !!BMy3:Cy10/y4/3/3/1;               [apply cast to target from currently acting stack]
    !!SSy10&y8>0:E3/y21 Py22;           [Restore spell effect and power to saved values]
  !!en;
!!en;
!!en;

 
*function to give extra attacks/shots to creatures
!?FU(OnBeforeBattleAction)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]

!!SN:W^Arena_Boss_Strikes^/0;           [reset flag or reset it at BG1]
!!BG:A?y4 N?y5;                         [get action and current stack]
!!SN&y4=7:W^Arena_%Y1_Extra_Shots^/?y6;
!!SN&y4=6:W^Arena_%Y1_Extra_Melee^/?y6;
!!FU&y6=0:E;

!!if|y4=6/y4=7;                         [if melee or shot]
!!if&y5>=21/y5<=41/y6>0;                [If attacking between 21 and 41]
  !!SN:W^Arena_Boss_Strikes^/dy6;
!!en;
!!en;

!?BG1&i^Arena_Fight^;                   [Trigger to handle extra shoots and additional strikes]
!!SN:W^Arena_Boss_Strikes^/0;           [reset flag or reset it at BG1]

!?FU(OnStartOrLoad)&i^Arena_Mod_Active^=1;
!!SN:L^EraPlugins\erm_hooker.era^/?y1;
!!FU&y1=0:E;
!!SN:Ay1/^SetHook^/?y2;
!!SN:Ey2/1/7381311/(Arena_Boss_Extra_Strike); [hook at <after metee_attack>]
!!SN:Ey2/1/4456346/(Arena_Boss_Extra_Shot);   [hook at <after shoot>]

!?FU(Arena_Boss_Extra_Strike)&i^Arena_Fight^; Extra Melee Attack
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]          
!!SN:X?y1/1;                            [edi]
!!SN:W^Arena_Boss_Strikes^/?y30;
!!SN:W^Arena_Boss_Strikes^/0;
!!FU&y30<1:E;
!!UN:Cy1/4/?y10;                        [y10=A_STACK]
!!VRy2:Sy1+8;
!!UN:Cy2/4/?y3;                         [y3=ebp]
!!VRy4:Sy3+8;                           [ebp+8]
!!UN:Cy4/4/?y5;                         [y5=[ebp+08]
!!VRy6:Sy1+4;
!!UN:Cy6/4/?y20;                        [y20=D_STACK]10#]
!!VRy11:Sy10+52;                        [11#]
!!VRy12:Sy10+76;
!!VRy13:Sy10+656;
!!VRy14:Sy10+688;
!!VRy15:Sy10+704;
!!VRy21:Sy20+52;
!!VRy22:Sy20+76;
!!UN:Cy11/4/?y31;                       [18#]
!!UN:Cy12/4/?y32;
!!UN:Cy13/4/?y33;
!!UN:Cy14/4/?y34;
!!UN:Cy15/4/?y35;
!!UN:Cy21/4/?y41;
!!UN:Cy22/4/?y42;
!!FU|y31<0/y32<1/y41<0/y42<1/y33>0/y34>0/y35>0:E;
!!SN:E4461360/2/y10/y20/y5;
!!VRy30:-1;
!!SN&y30>0:G18;


!?FU(Arena_Boss_Extra_Shot)&i^Arena_Fight^; Extra Shoot Attack
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:X?y1/1;                            [edi]
!!SN:W^Arena_Boss_Strikes^/?y30;
!!SN:W^Arena_Boss_Strikes^/0;
!!FU&y30<1:E;
!!VRy2:Sy1+4;                           [esi]
!!UN:Cy2/4/?y10;                        [A_STACK]
!!VRy3:Sy1+16;                          [ebx]
!!UN:Cy3/4/?y20;                        [D_STACK]
!!VRy11:Sy10+52;                        [8#]
!!VRy12:Sy10+76;
!!VRy13:Sy10+656;
!!VRy14:Sy10+688;
!!VRy15:Sy10+704;
!!VRy16:Sy10+216;
!!VRy21:Sy20+52;
!!VRy22:Sy20+76;
!!UN:Cy11/4/?y31;                       [BM:T]16#]
!!UN:Cy12/4/?y32;                       [BM:N]
!!UN:Cy13/4/?y33;                       [BM:G62]
!!UN:Cy14/4/?y34;                       [BM:G70]
!!UN:Cy15/4/?y35;                       [BM:G74]
!!UN:Cy16/4/?y36;                       [BM:U3]
!!UN:Cy21/4/?y41;                       [BM:T]
!!UN:Cy22/4/?y42;                       [BM:N]
!!FU|y31<0/y32<1/y41<0/y42<1/y33>0/y34>0/y35>0/y36<1:E;
!!SN:E4453920/2/y10/y20;
!!VRy30:-1;
!!SN&y30>0:G16;


**********************************Random attack Multishot*******************************************
Can shot multi targets at once

!?FU(ACM_OnAfterShoot)&i^Arena_Fight^; Extra Melee Attack
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!#VA(atkStack:x) (defStack:x);

!!BA:Q?y1; !!FU&y1=1:E;                 [Exit in Quick Combat]
!!VRy1:Sx1;                             [acting stack]
!!BMx1:I?y2;                            [Acting Side Monster]

!!BMx1:T?y3 P?y9;                       [Get Creature Number and current battlefield position]
!!if&y2=1;                              [if the correct creature or defeding side]
  !!SN&y2=1:W^Arena_Boss_Shots_Def^/?y20;[get number of random extra shots]
  !!FU&y20=0:E;                         [exit if no extra shot is available]
  !!SN&y2=1:W^Arena_Boss_Shots_Def^/d-1;[reduce number of extra shots by 1]
  !!BMx2:I?y30;
  !!FU(ACM_GetRandomLivingStackFromSide):Py30/?y10/(NO_STACK)/(TRUE)/(FALSE);
  !!FU(BattleStack_Shoot)&y10>(NO_STACK):Py1/y10; [call fct from stdlib that gives extra attack and pass attacker and target stack]
!!en;

!?FU(OnCombatRound)&v997>=0/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Random_Shots^/?y2;
!!SN&y2>0:W^Arena_Boss_Shots_Def^/y2;   [starts every battle round with x extra shot]


// Get a valid stack from a side
; by Archer30
; This is better than FU(AC_Function_55) as it consideres stack to ignore/living status, also possible to ignore blind/stoned/paralysed stacks
!?FU(ACM_GetRandomLivingStackFromSide);
!#VA(side:x);                           [Side of the stack]
!#VA(result:x);                         [Return value. Random stack number of the side]
!#VA(exceptionStack:x);                 [Optional. Stack number not counting as a valid result]
!#VA(ignoreControlled:x);               [Optional. Boolean. Ignore controlled (Blind, Stone and Paralysis) units]
!#VA(isFromArmy:x);                     [Optional. Boolean. Value for setting whether only stacks from a hero's army are considered as valid]

; Initialization
!!VR(result):S(NO_STACK);
!!FU:A?(numArgs:y);
!!VR(exceptionStack)&(numArgs)<3:S(NO_STACK);
!!VR(ignoreControlled)&(numArgs)<4:S(FALSE);
!!VR(isFromArmy)&(numArgs)<5:S(FALSE);

; Set up a local array for all the living stacks of the side
!!FU(NewIntArray):P?(livingStacks:y);   [Create the array]
!!VR(firstStack:y):S(side) *(BATTLE_STACKS_PER_SIDE);
!!VR(lastStack:y):S(firstStack) +20;

; Loop through all the stacks, add any valid stack number to the array
!!re i/(firstStack)/(lastStack);
  !!BMi:T?(type:y) N?(num:y);

  !!if&(type)>(NO_MON)/(type)<>(MON_ARROW_TOWERS)/(num)>0/i<>(exceptionStack);
    !!if&(ignoreControlled);
      !!BMi:G(SPELL_BLIND)/?(blindTurns:y)/d G70/?(stoneTurns:y)/d G74/?(paralyzeTurns:y)/d;
      !!co|(blindTurns)>0/(stoneTurns)>0/(paralyzeTurns)>0;
    !!en;

    !!if&(isFromArmy);
      !!BMi:O?(armySlot:y);
      !!FU(Array_Push)&(armySlot)>-1:P(livingStacks)/i; [Push the found stack id to the end of the array]
    !!el;
      !!FU(Array_Push):P(livingStacks)/i;
    !!en;
  !!en;
!!en;

; Check if the array has at least one item
!!SN:M(livingStacks)/?(size:y);

; Return a random item from he array if applicable
!!if&(size)>0;
  !!FU(Array_Shuffle):P(livingStacks);  [Shuffle the array]
  !!FU(Array_Pop):P(livingStacks)/?(result); [Get the last item of the array and remove the item from the array]
!!en;


**********************************Random melee attack***********************************************
Set the number of times the Boss attacks an additional target in melee
!?FU(ACM_OnAfterMelee)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Additional_Melee^/?y2; !!FU&y2=0:E;

!!SN:W^Arena_Boss_Extra_Melee_Strike^/?y30; !!FU&y30=0:E; [Exit if flag not active]
!!BA:Q?y1; !!FU&y1=1:E;                 [Exit in Quick Combat]
!!VRy1:Sx1;                             [acting stack]
!!BMx1:I?y2;                            [Acting Side Monster]
!!BMx1:T?y3 P?y9;                       [Get Creature Number and current battlefield position]
!!if&y2=1/x1=21;                        [if defending side and attacking stack ID 21]
  !!SN:W^Arena_Boss_Extra_Melee_Strike^/d-1; [reduce strike by one]
  !!FU(ACM_GetRandomLivingStackFromSide)&y30>0:P0/?y10/(NO_STACK)/(TRUE)/(FALSE);
  !!FU(ACM_BattleStack_MeleeAttack)&y30>0:Px1/y10/0; [make the stack attack another stack in melee combat]
!!en;

!?FU(OnCombatRound)&v997>=0/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Additional_Melee^/?y2; !!FU&y2=0:E;
!!SN:W^Arena_Boss_Extra_Melee_Strike^/y2;[starts every battle round with x extra shot]


// Initiate a melee attack
!?FU(ACM_BattleStack_MeleeAttack)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!#VA(atkStack:x) (defStack:x);
!#VA(direction:x);                      [0 for upper, 1 for forward and 2 for lower]

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(value:y);
!!VR(value2:x):S(atkStack) *1352 +21708 +(value);
!!VR(value3:x):S(defStack) *1352 +21708 +(value);
!!SN:E4461360/2/(value2)/(value3)/(direction);


****************************************************************************************************
*Set Resurrect script, lets creatures revive with a given HP value
!?FU(ACM_OnAfterMelee)&i^Arena_Fight^;
!#VA(atkStack:x) (defStack:x);
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E;
!!SN:W^Arena_%Y1_Resurrect_Value^/?y2; !!FU&y2=0:E;
!!SN:W^Arena_%Y1_Resurrect_Times^/?y7; !!FU&y7=0:E;
!!BM(atkStack):N?y3;
!!BM(defStack):N?y4;
!!BM(defStack):T?y5 N?y10 I?y11;        [get defending stack type, number and side]
!!if&(defStack)=21/y10=0/y7>0;  
  !!FU(Arena_Boss_Stack_Resurrect):P(defStack)/y2/0/1; [call resurrect fct if unit is dead]
  !!SN:W^Arena_%Y1_Resurrect_Times^/d-1;[reduce counter by one]
  !!SN&y1=23:W^Arena_23_Once_Flag^/1;   [extra flag for Arena 23]
!!en;

!?FU(Arena_Boss_Stack_Resurrect); [revive a stack By 007]
**x1=stack
**x2=hp recovered [one will revive as long as >0]
**x3=revival type [0=live,1=undead,2=golem,3=dragon,4=all]
**x4=caster side[0=left,1=right]
!!FU|x1<0/x1>41/x2<1/x3<0/x3>4:E;
!!BMx1:T?y1 N?y2 B?y3 O?y4 L?y6 F?i;
!!VRy7:Si &1;
*!VRy8:Si &16;
*!VRy9:Si &262144;
!!VRy10:Si &8388608;
!!VRy11:Si &-2147483648;
!!VRy12:Si &268435456;
!!FU|y1<0/y1=124/y1=149/y10>0/y12>0:E; 

!!FU&y2>=y3/y6=0:E;       
*!FU&x3=0/y8=0:E; Exit if Alive flag   
*!FU&x3=1/y9=0:E; Exit if Undead flag   
!!FU&x3=2/y8>0:E;    
!!FU&x3=2/y9>0:E;
!!FU&x3<3/y11<>0:E;  
!!UN:C6919200/4/?y13;    
!!VRy14:Sx1 *1352 +21708 +y13;
!!SN:E4481984/2/y14;
!!BMx1:P?y15;
!!VRy16:Sv1;      
!!if&y2=0:;         
!!BU:Ey15/?y17;
!!FU&y17>-1:E;
!!if&y7=1:;        
!!BU:Ey16/?y18;
!!FU&y18>-1:E;
!!en:;
!!en:;
!!SN:E5929072/2/y13/y14/x2/0;  
!!VRz-9:S^^;        
!!VRz-9&x3=0:S^Resurect.wav^;
!!VRz-9&x3=1:S^Animdead.wav^;
!!VRz-9&x3=2:S^Removeob.wav^;
!!VRz-9&x3=3:S^Sacrif1.wav^;
!!VRz-9&x3=4:S^Regener.wav^;
!!SN&i^battle_isVisible^:Pz-9;


****************************************************************************************************
*Chance to Evade/Dodge melee or ranged attacks*
!?MF1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Dodge_Value^/?y2; !!FU&y2=0:E;

!!UN:C42149568/4/?y10;                  [Physical Damage]
!!if|y10=4462398/y10=4455011/y10=4456676/y10=4454752;[Only run when melee or shooting damage]
  !!BG:A?y3;                            [get battle action]
    !!if|y3=6/y3=7;                     [If Melee or Range]
    !!MF:N?y4;                          [damage receiving stack]
    !!BMy4:I?y5 S?y6;                   [Monster Side Index type and speed]
    !!if&y4>=21/y3<=41/y5=1;            [if any defender stack and defender side]
      !!VRy20:S0R100;                   [Generate Random number]
      !!VRy22:Sy2;                      [Set the chance to dodge to +XX%]
      !!if&y20<=y22;                    [chance is ]
        !!VRz-1:S^AIRSHELD^;
        !!SN&i^battle_isVisible^:Pz-1;
        !!MF:E0;                        [set damage to zero]
        !!MF:F0;                        [set final damage to zero]
        !!SN:T^raid_monster.Air Dodge^/?z-1;
        !!BU&i^battle_isVisible^:Mz-1;  [show message in log]
      !!en;
    !!en;
  !!en;
!!en;


****************************************************************************************************
*Regeneration echantment. Regenerates around XX% of max HP every round when its the commanders turn
!?FU(OnBattleStackRegeneration)&i^Arena_Fight^;
Standard value is currently xx%
!#VA(stackId:x) (finalValue:x) (stdValue:x);
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Regeneration_Value^/?y2; !!FU&y2=0:E;
!!if&x1=21;                             [for any Arena Boss]
  !!BM(stackId):T?y3 H?y4;              [get monster type and health]
  !!VRy4:*y2:100;                       [percent of health]
  !!VR(finalValue)&(stackId)>=21/(stackId)<=41:Sy4; [set regeneration value]
!!en;


****************************************************************************************************
*Summoner can call different stacks to the battle field 
Trigger maybe different, like ObtainsTurn, OnNewRound, or even BG
!?FU(OnBattleStackObtainsTurn)&i^Arena_Fight^;
!#VA(stackSide:x) (stackInd:x);
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Summon_Creature_ID^/?y2; !!FU&y2=0:E;
!!SN:W^Arena_%Y1_Summon_Creature_Numbers^/?y3; !!FU&y3=0:E;

!!VRy20:Si^battle_current_stack^;
!!BMi^battle_current_stack^:T?y4 I?y5 H?y6; [Get type and side of current battlestack and health points]
!!FU|y4=0/y20<>21:E;                    [Exit if it is not the Arena Boss]

!!FU(Arena_Boss_CheckIfPossibleToSummon):Py5/?y21; !!FU&y21=0:E; [Exit if conditions not met]

!!re i/0/0;                             [here you can set the times the summoning should happen in the loop]
  !!VRy70:S0R185;                       [random battlefield position]
  !!FU(WOG_GetValidPositionForStack):Py70/86/1/?y71/2; [by wessonsm & Archer30]
  !!if&y71>-1;
    !!BU:Sy2/y3/y71/1/0/0;              [Summon elementals at a random position]
    *!FU(Arena_Boss_Set_Summon_Stats):Py71; if the creature should have increased stats you can set here
  !!en;
!!en;

!?FU(Arena_Boss_Set_Summon_Stats);
x1=battlefield position
!!BU:Ex1/?y1;                           [get stack number from battlefield position]
!!VRy22:Si^timerMonth^;                 [Put Current Week in y22]
!!if&y1>21;
  !!BMy1:H50 A20 D20 U1/25 U2/75 U3/0 E0;[Set Stats and Set casts and shots to zero]
  !!BMy1&y22>5:H75 A30 D30 U1/50 U2/100 U3/0 E0; [Set Stats and Set casts and shots to zero]
  !!BMy1&y22>10:H100 A40 D40 U1/75 U2/125 U3/0 E0; [Set Stats and Set casts and shots to zero]
!!en;

; Check if there are few than 21 stacks on the battlefield
!?FU(Arena_Boss_CheckIfPossibleToSummon);
!#VA(side:x) (isEligible:x);

!!VR(isEligible):S(FALSE);
!!UN:C6919200/4/?(value:y);
!!VR(address:y):S(side) *4 +21692 +(value);
!!UN:C(address)/4/?(stackNum:y);
!!VR(isEligible)&(stackNum)<=20:S(TRUE);


****************************************************************************************************
*to act additional times in combat
!?FU(OnCombatRound)&i^battle_round^>=0/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Act_Twice^/?y2;  !!FU&y2=0:E; [Set amount of actings per round]

!!SN:W^Arena_Boss_acting_stack^/-1;     [reset act twice counter and battle stack each new battle round]
!!if&i^battle_round^>=0;
  !!re i/0/41;                          [loop all stacks and reset]
    !!SN:W^Arena_Boss_abilityCounter_%i^/0;
  !!en;
!!en;

!?FU(OnBeforeBattleAction)&i^battle_round^>=0/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Act_Twice^/?y2;  !!FU&y2=0:E; [Set amount of actings per round]

!!SN:W^Arena_Boss_acting_stack^/-1;
!!BG:A?y1 N?y2 E?y6;                    [Get action type and stack number and attacked stack]
!!FU&y1<>2/
     y1<>6/
     y1<>7/
     y1<>9/
     y1<>10/
     y1<>11:E;                          exit for wrong condition

!!BMy2:T?y3 I?y4 N?y5;                  [get type and side]
!!if|y2=21/y2=22/y2=23;                 [If raid boss]
  !!VRy20:S0R1;                         [make a coin flip]
  !!if&y6>=0;                           [if a valid target stack]
    !!BMy6:N?y13 F?y14 F?y15;           [get number of attacked monsters]
    !!VRy14:&4194304;                   [Check for Summoning flag]
    !!VRy15:&12582912;                  [Check for Clone flag]
    !!VRy20|y13=1/y14>0/y15>0:S1;       [Set higher chance to act again if just one unit, or summoned or cloned]
    !!VRy20:S1;
  !!en;
  !!SN&y20=1:W^Arena_Boss_acting_stack^/y2; [set and save stack that should act twice]
!!en;


!?FU(OnBattleActionEnd)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Act_Twice^/?y1;  !!FU&y1=0:E; [Set amount of actings per round]

!!SN:W^Arena_Boss_acting_stack^/?y1;
!!if&y1>=21/y1<=41;                     [if a valid defender battle stack]
  !!SN:W^Arena_Boss_abilityCounter_%Y1^/?y2; [check ability counter]
  !!BMy1&y2=0:Fd~(MON_FLAG_ACTED);      [set act twice flag for stack]
  !!SN:W^Arena_Boss_abilityCounter_%Y1^/d-1; [reduce ability counter]
  !!VRz1&y2=0:S^{~r}Arena Boss acts again{~}^;
  !!BU&y2=0:Mz1;
!!en;

!?FU(OnBeforeBattleStackTurn)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_Act_Twice^/?y2;  !!FU&y2=0:E; [Set amount of actings per round]

!!SN:W^Arena_Boss_acting_stack^/?y1; 
!!FU&y1=-1:E;                           [exit if no valid stack]
!!BMy1:T?y2 N?y3;                       [get type and number]
!!FU|y2=-1/y3<=0;                       [exit if not valid]
!!SN:W^Arena_Boss_acting_stack^/x1;     [set stack that should act twice]


****************************************************************************************************
*AOE attack for Arena Boss. 50% damage to surrounding target units
!?MF1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]
!!SN:W^Arena_%Y1_AOE_Damage_Value^/?y2; !!FU&y2=0:E; [set percent as AOE damage]

!!UN:C42149568/4/?y10;                  [Get Physical Damage type]
!!FU&y10<>4462398:E;                    [Melee Damage]

!!BG:A?y1;                              [Aktion in y1]
!!FU&y1<>6:E;                           [or Melee]
!!BG:N?y1;                              [Attacking Stack]
!!if&y1>=21/y1<=41;                     [if any defending stack]
  !!MF:N?y4;                            [damage receiving stack]
  !!BMy4:T?y40;                         [Get Type in y40]
  !!BMy1:I?y5;                          [Side index as for heroes (0:left, 1:right)]
  !!BMy4:P?y1 I?y30;                    [get target position and Side]
  !!MF:F?y10;                           [get damage of melee attack]
  !!VRy10:*y2:100;                      [Make xx% of damage]

  !!FU(AC_Function_126):Py1/0/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]

  !!FU(AC_Function_126):Py1/1/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]

  !!FU(AC_Function_126):Py1/2/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]

  !!FU(AC_Function_126):Py1/3/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]

  !!FU(AC_Function_126):Py1/4/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]

  !!FU(AC_Function_126):Py1/5/?y3;
  !!BU&y3>=0/y3<=186:Ey3/?y8;           [-2 if there is alive stack at that position]
  !!BMy8&y8>=0/y8<=41/y4<>y8:I?y40;     [apply damage to surrounding creatures]
  !!BMy8&y8>=0/y8<=41/y4<>y8/y40=y30:Ky10; [apply damage to surrounding creatures if same side]
!!en;


****************************************************************************************************
*Code to block damage
!?MF1&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]

!!SN:W^Arena_%Y1_Block_Chance^/?y2; !!FU&y2=0:E; [set the chance by which the stack will block]
!!SN:W^Arena_%Y1_Block_Value^/?y3; !!FU&y3=0:E; [set the amount of defelect in percent from 1 to 100]

!!VRy10:S0R100;                         [generate random number]
!!FU&y10>y2:E;                          [make a XX percent chance to work]
!!MF:W?y80;
!!FU&y80>0:E;                           [exit if wrong damage type]
!!BG:A?y1;                              [get battle action]
!!if|y1=6/y6=7;                         [or melee or shot]
  !!MF:N?y4;                            [damage receiving stack]
  !!FU&y4<>21:E;                        [So it only works for Arena Boss]
  !!MF:F?y6;                            [Damage Dealt]
  !!VRy6:*y3:100;                       [make XX% of damage]
  !!if&y6>0;                            [if positive damage]
    !!MF:Fy6;                           [apply reduced damage]
    !!VRz-1:S^CRUSDFND.wav^;            [find block sound]
    !!SN&i^battle_isVisible^:Pz-1;      [play block sound]
    !!BMy4&i^battle_isVisible^:V84;     [play block animation]
  !!en;
!!en;


****************************************************************************************************
*Apply spells to Arena Boss at battlestart, at stack ID 21
!?FU(OnBattleRound)&v997=0/i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1; !!FU&y1=0:E; [Exit if it is not Arena Fight]

!!SN:W^Arena_%Y1_BuffSpell_ID_1^/?y2; !!FU&y2=0:E; 
!!SN:W^Arena_%Y1_BuffSpell_Power_1^/?y3; !!FU&y3=0:E; 

!!SSy2:E3/?y20;                         [get effect for expert rank]
!!SSy2:E3/y3;                           [set new effect value]
!!BM21:My2/99/3;                        [apply spell to stack 21]
!!BM22:My2/99/3;                        [apply spell to stack 22]
!!BM23:My2/99/3;                        [apply spell to stack 23]
!!BM24:My2/99/3;                        [apply spell to stack 24]
!!SSy2:E3/y20;                          [reset spell effect]

!!SN:W^Arena_%Y1_BuffSpell_ID_2^/?y2; !!FU&y2=0:E; 
!!SN:W^Arena_%Y1_BuffSpell_Power_2^/?y3; !!FU&y3=0:E; 

!!SSy2:E3/?y20;                         [get effect for expert rank]
!!SSy2:E3/y3;                           [set new effect value]
!!BM21:My2/99/3;                        [apply spell to stack 21]
!!BM22:My2/99/3;                        [apply spell to stack 22]
!!BM23:My2/99/3;                        [apply spell to stack 23]
!!BM24:My2/99/3;                        [apply spell to stack 24]
!!SSy2:E3/y20;                          [reset spell effect]