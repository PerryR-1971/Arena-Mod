ZVSE2
****************************************************************************************************


                                          Arena Mod



****************************************************************************************************
*In each town there is an Arena that can be visited by the player.
As in the M&M games the Arena offers to fight creatures for reward. There are certain challenges 
available. The player cannot loose troops or be defeated in these battles. All troops will be restored
after battle.
!#SN:W^Arena_Mod_Active^/1; Set Arena Mod active



*Set difficulty when entering the game
!?FU(OnGameEnter)&i^timerIsAi^=0/i^timerDay^=1/i^Arena_Mod_Active^=1;
!!FU:E;                                 [temporarily disabled]
!!FU(Arena_Boss_Settings_DL):P;

When clicking on System Icon at Adventure Map run options again
!?CM&1000&i^Arena_Mod_Active^=1;
!!FU:E;                                 [temporarily disabled]
!!CM:F?v3 I?v4 S?v5;                    [where we click and click subtype]
!!if&v3=512/v4=10;                      [if right click on System Icon  are clicked]
!!FU(Arena_Boss_Settings_DL):P;
!!en;


!?FU(Arena_Boss_Settings_DL);
!!SN:T^AC_Arena.diff_header^/?z1;       [set text in header]
!!SN:T^AC_Arena.diff_level1^/?z2; 
!!SN:T^AC_Arena.diff_level2^/?z3; 
!!SN:T^AC_Arena.diff_level3^/?z4;
!!SN:T^AC_Arena.diff_level4^/?z5; 

!!IF:G1/3/2/1/2/3/4/5/0/0/0/0/0/0/0/;   [Show Choice Dialouge]

!!SN&v3=1:W^Arena_Boss_Difficulty^/1;   [Easy]
!!SN&v3=2:W^Arena_Boss_Difficulty^/2;   [Normal standart and initial value]
!!SN&v3=4:W^Arena_Boss_Difficulty^/3;   [Hard]
!!SN&v3=5:W^Arena_Boss_Difficulty^/4;   [Extreme]


*Open Arena when clicking on marketplace 
!?FU(OnTownMouseClick)&i^Arena_Mod_Active^;                 [Town Screen Trigger]

!!CM:T?y1 F?y2 I?y3 S?y4;               [y1, y2, y3, y4, where we click and click subtype]
!!FU&y4<>14:E; !!FU&y2<>512:E;          []
!!if&y1=512/y2=512/y4=14/y3=14;         [if right click on marketplace]
  !!CA-1:H1/?y5;                        [y5, town Visitor]
  !!FU|y5<0/y5>155:E;                   [Exit if no valid hero number]
  !!SN:W^Arena_Hero^/y5;                [Save hero that clicks the building]
  !!HEy5:O?y50;                         [Get Hero Owner]
  !!SN:W^Arena_Attacker_Owner^/y50;     [Save Owner for later]
  !!FU(Arena_Mod_Main):Py5/1/0; [Jump to arena function Pass hero and say we enter from a town]
!!en; 


*Open Arena when clicking on the new GEM town buttons
!?FU(gem_OnOpenButtonDlg)&i^Arena_Mod_Active^; [Function from GEM Mod that is called when clicking the new buttons]
  !!CA-1:H1/?y5;                        [y5, town Visitor]
  !!FU|y5<0/y5>155:E;                   [Exit if no hero]
  !!SN:T^AC_Arena.Name_Town_Button^/?z22;[z20]
  !!SN:T^AC_Arena.Name_Town_Button_Desc^/?z23; [z21]
  !!SN:W^Arena_Hero^/y5;                [Save hero that clicks the building]
  !!HEy5:O?y50;                         [Get Hero Owner]
  !!SN:W^Arena_Attacker_Owner^/y50;     [Save Owner for later]
  !!FU(gem_AddTownButton):P^comartifact.def^/64/64/^%Z22^/^%Z23^/^%Z23^/(Arena_Mod_Main)/y5/1/0; [Arena button]


*Open Arena when visiting an actual Arena on the map 
!?OB4&1000&i^Arena_Mod_Active^; 

*!PO998:N?y1; *!FU&y1=1:E;              [Exit if Arena has been visited] Test
!!HE-1:N?y5;                            [get current hero ID]
!!FU|y5<0/y5>155:E;                     [Exit if no valid hero number]
!!SN:W^Arena_Hero^/y5;                  [Save hero that clicks the building]
!!HEy5:O?y50;                           [Get Hero Owner]
!!SN:W^Arena_Attacker_Owner^/y50;       [Save Owner for later]
*!OB998:D;                              [disable object for player]
*!OB998:M-1/1/0;                        [disable next text message from object]
!!FU(Arena_Mod_Main):Py5/2;             [Jump to arena function nd say we enter from a Arena on the map]
    
*?HM-1;
*!HE-1:N?y5;  
*!re i/0/31;                     
*!HEy5:V7/i/1;
*!en;


*Arena Main Dialouge
!?FU(Arena_Mod_Main);
x1=Hero ID, x2= 1 means we enter from a town, x2=2 means we enter from Arena map object

  *!FU(Set_Arena_Descriptions):P;       [Sets all Arenas descriptions like text, rewards, creatures]
  !!SN:W^Arena_Dialogue_Call^/x2;       [set variable that holds information from where we enter the Arena main DL]

  !!DL379:N^arena2.txt^;                [parse dialogue]
  !!SN:T^AC_Arena.Left_Button^/?z22;    [z22]
  !!SN:T^AC_Arena.Right_Button^/?z23;   [z23]
  !!SN:T^AC_Arena.Header_Text^/?z80;    [z80]

  !!DL379:A4/3/z22/1;                   [Left button]
  !!DL379:A3/3/z23/1;                   [Right button]
  !!DL379:A5/3/z80/1;                   [Welcome text]
  !!DL379:A10/3/^^/1;                   [empty text field when DL is created]
  !!SN:W^Arena_Challenge_Number^/0;     [reset variable when DL is created]

  !!VRy1:S1R1;                          [Choose a random page to open between 1 and 2]
  !!SN&y1=1:W^Arena_Pages^/1;           [When DL is created start with first page]
  !!SN&y1=2:W^Arena_Pages^/2;           [When DL is created start with first page]

*Set name of Arenas that appear in the text field
  
  !!re i/1/45;
  !!FU(Check_Arena_Requirements):Pi;
  !!en; 
  
  !!re i/20/34;  
    !!VRr&y1=1:Si-19;                   [make correct offest for arena number first page]
    !!VRr&y1=2:Si-4;                    [make correct offest for arena number second page]
    !!VRzi:Ss^Arena_%r_CB_Name^;        [get name of arena]
    !!SN:W^Arena_%r_locked^/?y50;       [Get locked status of the arena]
    !!VRzi&y50>0:S^{~Grey}%Zi{~}^;      [all names of arenas start in grey]
    !!DL379:Ai/3/zi/1;                  [Put Name of the First Challenge to the DL field]
  !!en;

  !!re i/50/64;
    !!DL379:Ai/4/1;                     [Set colour of Cadres, different colours are available]
  !!en;

  !!if&y1=1;
    !!DL379:A6/3/^Easy^/1;              [Set name/category of first colume]
    !!DL379:A7/3/^Medium^/1;            [Set name/category of second colume]
    !!DL379:A8/3/^Hard^/1;              [Set name/category of third colume]
  !!en;

  !!if&y1=2;
    !!DL379:A6/3/^Bossbattle^/1;        [Set name/category of first colume]
    !!DL379:A7/3/^Bossbattle^/1;        [Set name/category of second colume]
    !!DL379:A8/3/^Bossbattle^/1;        [Set name/category of third colume]
  !!en;

*Here save all heroes parameter so they can be restored after combat*
  !!HEx1:P?y30/?y31/?y32;               [Check current hero position]
  !!COx1:D?y40;                         [Check if Commander is Dead or Alive]
  !!SN:W^Arena_Commander_Check^/y40;    [Save commander status]
  *!HEx1:I?y33/1;                       [Get Hero Spell Points]
  *!SN:W^Arena_Hero_Spell_Points^/y33;  [Save Spell Points]
  !!re i/0/6;                           [cycle hero slots and save army to restore it later]
    !!HEx1:C0/i/?y20/?y21;              [check slot]
    !!SN:W^Arena_Mod_Army_ID_%i^/y20;   [save creature ID]
    !!SN:W^Arena_Mod_Army_Amount_%i^/y21;[save amount of creatures]
    !!HEx1:C0/i/?y97/?y98/?y25/12;      [Get Stack Experience from all slots in heros army]
    !!SN:W^Arena_Mod_Army_StackExp_%i^/y25; [save Stack Experience from all slots in heros army]
    !!COx1&i<>6:A3/i/?y66/?y67;         [Get com artifact ID and battles won]
    !!SN&y66>0:W^Arena_Com_Arti_%i^/y67;[save number of battles won from Com artifacts]
  !!en;  

  !!VRz1:S^Human Training Center 01.wav^; !!SN:Pz1; [Play gold and door sound]

*Set Hint texts
  !!SN:T^AC_Arena.Hint_1^/?z40;  
  !!re i/20/34;  
    !!DL379:Hi/40/1;                    [Set hint text]
  !!en;  

  !!SN:T^AC_Arena.Hint_2^/?z41;    
  !!DL379:H1/41/1;                      [Set hint text]
  !!SN:T^AC_Arena.Hint_3^/?z42;    
  !!DL379:H5/42/1;                      [Set hint text]
  !!SN:T^AC_Arena.Hint_4^/?z43; 
  !!DL379:H13/43/1 H14/43/1;            [Set hint text]
  !!SN:T^AC_Arena.Hint_5^/?z44; !!SN:T^AC_Arena.Hint_6^/?z45;  
  !!DL379:H30722/44/1 H30721/45/1;      [Set hint text]

  !!if&y1=2;                            [if Boss Battle Page, hide orange defs]
    !!FU(DL_FindById):P379/?(dlgPtr:y);
    !!UN:C(dlgPtr)/4/?(dlg:y);
    !!re i/50/64;
      !!FU(gem_DlgItem_Show):Pi/0/0/0/(dlg); [hide all cadres]
    !!en;
  !!en;

  !!DL379:S;                            [Show dialouge]

  !!SN:W^Arena_Close_TownScreen^/?y50;
  !!if&y50=1/x2=1;
    !!CM:I30720;                        [Press Town Exit button to prevent grafik glitch]
  !!en; 


*proc the fight when town screen gets closed 
*On after any town visit and we come from the Arena Main DL then the fight starts
!$OB98&i^Arena_Mod_Active^;

!!SN:W^Arena_Close_TownScreen^/?y50;    [Get trigger to see if fight must be started]
!!SN:W^Arena_Challenge_Number^/?y10;    [Get Arena number so that we know which arena was selected]
!!SN:W^Arena_Hero^/?y5;                 [Get hero who is visiting the Arena]
!!FU(Arena_Fight_and_reward)&y50=1/y10>0:Py5/y10/y50; [jump to Arena fct and pass hero, arena number and where we come from]


!?FU(OnCloseTownScreen)&i^Arena_Mod_Active^;
!!SN:W^Arena_Close_TownScreen^/?y50;    [Get trigger to see if fight must be started]
!!SN:W^Arena_Challenge_Number^/?y10;    [Get Arena number so that we know which arena was selected]
!!SN:W^Arena_Hero^/?y5;                 [Get hero who is visiting the Arena]
!!FU(Arena_Fight_and_reward)&y50=1/y10>0:Py5/y10/y50; [jump to Arena fct and pass hero, arena number and where we come from]


*On after any map object Arena visit
!$OB4&i^Arena_Mod_Active^;

!!SN:W^Arena_Challenge_Number^/?y10;    [Get Arena number so that we know which arena was selected]
!!SN:W^Arena_Hero^/?y5;                 [Get hero who is visiting the Arena]
*!OB998&y10>0:M-1/1/0;                  [disable next text message from object]
*!PO998&y10>0:N1;                       [set control bit to 1, this will prevent from Arena to appear again]
!!FU(Arena_Fight_and_reward)&y10>0:Py5/y10/2; [jump to Arena fct and pass hero, arena number and where we come from]


!?FU(Arena_Fight_and_reward);

!!if&x2>0;    
  !!FU(Arena_Challenge):Px2/x1;         [Jump to Arena function and pass Arena number and Hero]
  !!SN&x3=1:W^Arena_Close_TownScreen^/0;  
  
*after the battle restore all parameters like army, commander and stack expirience
  !!CA-1&x3=1:P?y30/?y31/?y32;          [Get current town]
  !!HEx1&x3=2:P?y30/?y31/?y32;          [Get hero position]
  !!HEx1:O?y50;                         [Get Hero Owner]
  !!if&y50=-1;                          [If no Owner]
    !!SN:W^Arena_Attacker_Owner^/?y50;  [Save Owner for later]
    !!HEx1:Oy50;                        [set Owner again]
    !!HEx1:Py30/y31/y32/0;              [teleport hero to location again]
    !!SN:D;    
    !!OW:Ay50/x1;                       [set hero active again]
    !!UN:R1;                            [Redraw screen]
  !!en;

  !!re i/0/6;                           [cycle hero slots to restore creatures from before the fight]
   !!SN:W^Arena_Mod_Army_ID_%i^/?y20;
   !!SN:W^Arena_Mod_Army_Amount_%i^/?y21;
   !!HEx1:C0/i/y20/y21;                 [restore creatue in hero slots]
   !!SN:W^Arena_Mod_Army_StackExp_%i^/?y25; [save Stack Experience from all slots in heros army]
   !!HEx1:C0/i/?y88/?y88/y25/12;        [Get Stack Experience from all slots in heros army]
   !!SN:W^Arena_Com_Arti_%i^/?y67;      [save number of battles won from Com artifacts]
   !!COx1&i<>6:A3/i/?y66/y67;           [Set com artifact ID and battles won]
  !!en;

  !!SN:W^Arena_Commander_Check^/?y40;   [Check commander status from before Battle]
  !!COx1:Dy40;                          [Restore commmander status]

  !!FU(Arena_Mod_Reward):Px2/x1;        [In case of creature reward jump there]

  *!HEx1:I?y33/1;                       [Get Hero Spell Points]
  *!SN:W^Arena_Hero_Spell_Points^/?y34; [Get Spell Points]
  *!HEx1&y33<y34:Iy34/1;                [Restore Hero Spell points]
  !!SN:W^gem_Quick_Combat_Check^/1;     [Restore Quick Combat Message]
  !!SN:W^Current_Battle_Type%Y50^/8;    [Set to ask Type for current player]
!!el;

  !!SN:W^Arena_Close_TownScreen^/0;   
  !!SN:W^gem_Quick_Combat_Check^/1;     [Restore Quick Combat Message]
  *!SN:W^Current_Battle_Type0^/8;       [Set to ask Type for player 1]



*Set text and names for arena and handle left clicks in the Arena Main DL
!?DL&v998=379/v1000=13;                 [left click on the challenges to show short description in info field]

*!SN&v999=20:T^AC_Arena.Level_%X1_Short_Text^/?z10; [set text for 1st]
*!SN:T^AC_Arena.Header_Text^/?z80;      [z80]
*!DL379:A5/3/z80/1;                     [Welcome text]

!!if&v999>0/v999<=999;                  [only reacts to clicks on arena names]
!!SN:W^Arena_Pages^/?y99;               [Get number of page]
!!SN&y99>3:W^Arena_Pages^/1;            [Get number of page]
!!SN:W^Arena_Pages^/?y99;               [Get number of page]
!!DL379:A2/3/^Page:%Y99^/1;             [Show text in description field]

*play sounds
!!SN:W^Arena_Hero^/?y5;                 [Get hero who is visiting the Arena]
!!HEy5:R2/?y1;                          [Get sex of a hero Male=0, Female=1]

!!VRv999:-19;                           [make click ID fit to Arena numbers temporaily]
!!VRz50:Ss^Arena_%V999_CB_Name^;        [Set z-var to the name from the arena template]
!!VRv999:+19;

!!if&y1=0/z50<>^^;                      [If Male and Arena is created with a name]
  !!VRz2&v999>=20/v999<=24:S^LM110c.wav^; !!SN&v999>=20/v999<=24:Pz2; [weak] *play sound depending on which challenge you click]
  !!VRz2&v999>=25/v999<=29:S^LM110f.wav^; !!SN&v999>=25/v999<=29:Pz2; [easy]
  !!VRz2&v999>=30/v999<=34:S^LM111c.wav^; !!SN&v999>=30/v999<=34:Pz2; [worthy]
!!en;  

!!if&y1=1/z50<>^^;                      [If Female and Arena is created with a name]
  !!VRz2&v999>=20/v999<=24:S^LF110b.wav^; !!SN&v999>=20/v999<=24:Pz2; [weak] *play sound depending on which challenge you click]
  !!VRz2&v999>=25/v999<=29:S^LF110c.wav^; !!SN&v999>=25/v999<=29:Pz2; [easy]
  !!VRz2&v999>=30/v999<=34:S^LF111e.wav^; !!SN&v999>=30/v999<=34:Pz2; [worthy]
!!en; 

*set text in description field 
!!if&v999=13;                           [Left scroll button]  
  !!SN:W^Arena_Pages^/d-1;              [Get number of page]
  !!SN:W^Arena_Pages^/?y99;             [Get number of page]
  !!SN&y99<1:W^Arena_Pages^/3;          [Get number of page]
  !!DL379:A2/3/^%Y99^/1;                [Show text in description field]
  !!SN:W^Arena_Pages^/?y99;             [Get number of page]  
  !!DL379:A10/3/^^/1;                   [empty text field when DL is created]
!!en;

!!if&v999=14;                           [Right scroll button]
*set text in description field   
  !!SN:W^Arena_Pages^/d1;               [Get number of page]
  !!SN:W^Arena_Pages^/?y99;             [Get number of page]
  !!SN&y99>3:W^Arena_Pages^/1;          [Get number of page]
  !!DL379:A2/3/^%Y99^/1;                [Show text in description field]
  !!SN:W^Arena_Pages^/?y99;             [Get number of page]  
  !!DL379:A10/3/^^/1;                   [empty text field when DL is created]
!!en;


!!if&y99=1;                             [If on First Page]
  *First colum Easy
  !!VRy10&v999=20:S1;                   [Set arena number that corresponds to Arena_001_Fight, 1 means 001]
  !!VRy10&v999=21:S2;                   [Set arena number that corresponds to Arena_002_Fight, 2 means 002]
  !!VRy10&v999=22:S3;                   [...]
  !!VRy10&v999=23:S4;
  !!VRy10&v999=24:S5;

  *Second colum Medium
  !!VRy10&v999=25:S6;
  !!VRy10&v999=26:S7;
  !!VRy10&v999=27:S8;
  !!VRy10&v999=28:S9;
  !!VRy10&v999=29:S10;

  *Third colum Hard
  !!VRy10&v999=30:S11;
  !!VRy10&v999=31:S12;
  !!VRy10&v999=32:S13;
  !!VRy10&v999=33:S14;
  !!VRy10&v999=34:S15;

  !!DL379:A6/3/^Easy^/1;                [Set name/category of first colume]
  !!DL379:A7/3/^Medium^/1;              [Set name/category of second colume]
  !!DL379:A8/3/^Hard^/1;                [Set name/category of third colume]
!!en;

!!if&y99=2;                             [If on First Page]
  *First colum 
  !!VRy10&v999=20:S16;                  [Set arena number that corresponds to Arena_001_Fight, 1 means 001]
  !!VRy10&v999=21:S17;                  [Set arena number that corresponds to Arena_002_Fight, 2 means 002]
  !!VRy10&v999=22:S18;                  [...]
  !!VRy10&v999=23:S19;
  !!VRy10&v999=24:S20;

  *Second colum 
  !!VRy10&v999=25:S21;
  !!VRy10&v999=26:S22;
  !!VRy10&v999=27:S23;
  !!VRy10&v999=28:S24;
  !!VRy10&v999=29:S25;

  *Third colum 
  !!VRy10&v999=30:S26;
  !!VRy10&v999=31:S27;
  !!VRy10&v999=32:S28;
  !!VRy10&v999=33:S29;
  !!VRy10&v999=34:S30;

  !!DL379:A6/3/^Bossbattle^/1;             [Set name/category of first colume]
  !!DL379:A7/3/^Bossbattle^/1;          [Set name/category of second colume]
  !!DL379:A8/3/^Bossbattle^/1;              [Set name/category of third colume]
!!en;

!!if&y99=3;                             [If on First Page]
  *First colum 
  !!VRy10&v999=20:S31;                  [Set arena number that corresponds to Arena_001_Fight, 1 means 001]
  !!VRy10&v999=21:S32;                  [Set arena number that corresponds to Arena_002_Fight, 2 means 002]
  !!VRy10&v999=22:S33;                  [...]
  !!VRy10&v999=23:S34;
  !!VRy10&v999=24:S35;

  *Second colum 
  !!VRy10&v999=25:S36;
  !!VRy10&v999=26:S37;
  !!VRy10&v999=27:S38;
  !!VRy10&v999=28:S39;
  !!VRy10&v999=29:S40;

  *Third colum 
  !!VRy10&v999=30:S41;
  !!VRy10&v999=31:S42;
  !!VRy10&v999=32:S43;
  !!VRy10&v999=33:S44;
  !!VRy10&v999=34:S45;

  !!DL379:A6/3/^Perry^/1;               [Set name/category of first colume]
  !!DL379:A7/3/^Daemon^/1;              [Set name/category of second colume]
  !!DL379:A8/3/^Archer^/1;              [Set name/category of third colume]
!!en;

  !!SN&v999>=20/v999<=34:W^Arena_Challenge_Number^/y10; [Set number so that we know which arena was selected]
  !!FU(Set_Arena_Difficulty):Py5;       [pass hero ID]
  !!FU(Set_Arena_Descriptions):Py10;    [pass challenge number]
  !!FU(Set_Arena_Descriptions_right_click):Py10; [pass challenge number]

  !!VRz10|v999<20/v999>34:S^^;          [Set to no text if not clicked on levels]
  !!VRy50&y99=1:Sv999-19;               [make correrct offset for arena number on page 1]
  !!VRy50&y99=2:Sv999-19+15;            [make correrct offset for arena number on page 2]
  !!VRy50&y99=3:Sv999-19+30;            [make correrct offset for arena number on page 3]
  *!VRz10:Ss^Arena_%Y50_Short_Text^;    [Set text that will show in the description field]
  *!FU(Arena_mod_calc_scaling):Py5/?y90/y10;
  *!IF:M^y5 is%Y5 and y90 is%Y90^;

  !!VRz10:Ss^Arena_%Y50_Description^;   [Set text that will show in the description field]
  *!IF:M^Text field contains %V999^;
  !!DL379&v999>=20/v999<=34:A10/3/^%Z10^/1;               [Show text in description field]
  !!DL379|v999=13/v999=14:A10/3/^^/1;   [empty text field when left or right arrow is clicked]

  !!re i/20/34;                         [loop elements to set names]
    !!VRr&y99=1:Si-19;                  [make correct offest for arena number on page 1]
    !!VRr&y99=2:Si-19+15;               [make correct offest for arena number on page 2]
    !!VRr&y99=3:Si-19+30;               [make correct offest for arena number on page 3]
    !!VRzi:Ss^Arena_%r_CB_Name^;        [Set z-var to the name from the arena template]
    !!SN:W^Arena_%r_locked^/?y51;       [Get locked status of the arena]

    *!VRy51:S0; Test not locked

    !!VRzi&y51>0:S^{~Grey}%Zi{~}^;      [all names of arenas start in grey]
    !!DL379:Ai/3/zi/1;                  [Put Name of the First Challenge to the DL field]
  !!en;
  

*code to highlight the clicked arena name in red
  !!VRy60&y99=1:Sv999-19;               [offset page 1]
  !!VRy60&y99=2:Sv999-19+15;            [offset page 2]
  !!VRy60&y99=3:Sv999-19+30;            [offset page 3]
  !!VRzv999:Ss^Arena_%Y60_CB_Name^;     [get name from template]
  !!VRzv999:S^{~r}%Zv999{~}^;           [make name red]
  !!DL379&v999>=20/v999<=34:Av999/3/zv999/1; [set name of arena]
  !!DL379:A2/3/^Page:%Y99^/1;           [Show text in description field]
!!en;


*code to hide the defs when switching to boss battle page
!!SN:W^Arena_Pages^/?y1;                [Get number of page]
!!if&y1=1;                              [if Boss Battle Page, hide orange defs]
  !!FU(DL_FindById):P379/?(dlgPtr:y);
  !!UN:C(dlgPtr)/4/?(dlg:y);
  !!re i/50/64;
    !!FU(gem_DlgItem_Show):Pi/1/0/0/(dlg); [hide all cadres]
  !!en;
!!en;
!!if&y1=2;                              [if Boss Battle Page, hide orange defs]
  !!FU(DL_FindById):P379/?(dlgPtr:y);
  !!UN:C(dlgPtr)/4/?(dlg:y);
  !!re i/50/64;
    !!FU(gem_DlgItem_Show):Pi/0/0/0/(dlg); [hide all cadres]
  !!en;
!!en;

*Check certain requirements to enter the arena
  !!SN:W^Arena_Challenge_Number^/?y10;
  !!FU(Check_Arena_Requirements)Py10;

  !!SN:W^Arena_Hero^/?y5;               [Get hero who is visiting the Arena]
  !!HEy5:O?y6;                          [Get the Owner number and the hero level]
  !!SN:T^AC_Arena.Header_Text^/?z80;    [z80]
  !!DL379:A5/3/z80/1;                   [Welcome text]

*If left clicked on the fight button enter an arena that has been selected before
  !!if&v999=30722/v1000=13/y10>0;       [if left click on fight/confirm button]

    !!SN:W^Arena_%Y10_locked^/?y80;

    *!IF:M^Arena lock code is%Y80 and clicked Arena %Y10^;

    *!VRy80:S0; Test not locked

    !!DL380:N^arena3.txt^;              [parse dialogue]

    !!if&y80=1;                         [If player has not enough cold to pay the arena costs]
      !!SN:T^AC_Arena.Exit_Condition_1^/?z5;      
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 04.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en; 

    !!if&y80=2;                         [If arena has already been visited this week]
    !!SN:T^AC_Arena.Exit_Condition_2^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en; 

    !!if&y80=3;                         [If level requirement is to high]
    !!SN:T^AC_Arena.Exit_Condition_3^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en; 

    !!if&y80=4;                         [If level requirement is to low]
    !!SN:T^AC_Arena.Exit_Condition_4^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en; 

     !!if&y80=5;                        [If player has surpassed the allowed visits in this arena]
     !!SN:T^AC_Arena.Exit_Condition_5^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en; 

    !!if&y80=6;                         [If hero has surpassed the allowed visits in this arena]
    !!SN:T^AC_Arena.Exit_Condition_6^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en;

    !!if&y80=7;                         [If ACM is not active]
    !!SN:T^AC_Arena.Exit_Condition_7^/?z5;
      !!DL380:A2/3/z5/1;
      !!FU(DL_ShowPopup):P380;          [Show Popup DL]
      !!VRz1:S^Human Training Center 03.wav^; !!SN:Pz1; [Play gold and door sound]
      !!FU:E;                           [Exit]
    !!en;                    

*battle starts here if the player has all necessary requirements
*moved to on OnCloseTownScreen

      !!SN:W^Arena_Close_TownScreen^/1;
      !!SN:W^gem_Quick_Combat_Check^/0; [Disabel Quick Combat Message]
      !!SN:W^Current_Battle_Type%Y6^/1; [Set to never ask for quick battle]
      

      !!OW:Ry6/6/?y2;                   [Check Gold]
      !!VRy3:Si^Arena_%Y10_Costs^;      [Get gold cost for that challenge]
      !!VRy2:-y3;                       [Substract costs from players gold]
      !!OW:R-1/6/y2;                    [Reduce Gold from player]
      *!HEy5:Wd-200/1;                  [Reduce some movement from hero]
      !!VRz1:S^gold03.wav^; !!SN:Pz1;   [Play gold and door sound]
      !!VRy7:Si^timerWeek^;             [Set y7 to current week]
      !!SN:W^Arena_Owner_%Y6_Hero_%Y5^/y7; [If player plays set to current week]
      !!SN:W^Arena_Dialogue_Call^/?y60; [get variable that holds information from where we enter the Arena main DL]
      !!if&y60=1;
        !!CA-1:P?y30/?y31/?y32;         [Get town coordinates]
        !!CM:I30720;                    [Press Town Exit button to prevent grafik glitch]
      !!en;
      *!FU(Arena_Challenge):Py10/y5;    [Jump to Arena function and pass Arena number and Hero]      
      !!DL:C1;


!?FU(Check_Arena_Requirements);
This fct checks if an arena can be visited by the current hero. If not it will set a "locked variable" that can be later used
x1=Arena Number ID

  !!SN:W^Arena_Hero^/?y5;               [Get hero who is visiting the Arena]
  !!HEy5:O?y6 E?y11/?y12/1;             [Get the Owner number and the hero level]
  !!VRy7:Si^timerWeek^;                 [Set y7 to current week]
  !!SN:W^Arena_Owner_%Y6_Hero_%Y5^/?y8; [If player plays set to current week]
  !!SN:W^Arena_%X1_locked^/0;           [Reset lock from Arena]
  !!VRy13:Si^Arena_%X1_Level_Req^;      [Get Min level requirement of that challenge]
  !!VRy14:Si^Arena_%X1_Level_Req_Max^;  [Get Max level requirement of that challenge]
  !!VRy15:Si^Arena_%X1_Visits_Hero^;    [Get how often the hero can clear this creature bank]
  !!VRy16:Si^Arena_%X1_Visits_Player^;  [Get how often the player can clear this creature bank]

  !!SN:W^Arena_%X1_Hero%Y5_wins^/?y17;  [check how often this hero has won in this arena]
  !!SN:W^Arena_%X1_Player%Y6_wins^/?y18;[check how often this player has won in this arena]

  !!if&x1>0;                            [if left click on fight/confirm button]
    !!OW:Ry6/6/?y2;                     [Check Gold]
    !!VRy3:Si^Arena_%X1_Costs^;         [Get gold cost for that challenge]

    !!if&y3>y2;                         [If you dont have enough gold]
      !!SN:W^Arena_%X1_locked^/1;  
    !!en;

    !!if&y8>=y7;                        [If arena has already been visited this week]
      !!SN:W^Arena_%X1_locked^/2;
    !!en;

    !!if&y13>y12;                       [If level requirement is to high]
      !!SN:W^Arena_%X1_locked^/3;
    !!en;

    !!if&y14<y12/y14<>0;                [If level requirement is to low]
      !!SN:W^Arena_%X1_locked^/4;
    !!en;

    !!if&y18>=y16;                      [If player has surpassed the allowed visits in this arena]
      !!SN:W^Arena_%X1_locked^/5;
    !!en;

    !!if&y17>=y15;                      [If hero has surpassed the allowed visits in this arena]
      !!SN:W^Arena_%X1_locked^/6;
    !!en;

    !!if&x1>=16/x1<=30/i^Advanced_Classes_Mod_Active^=0; [Bossbattle Arena can only be visited when ACM is active]
      !!SN:W^Arena_%X1_locked^/7;
    !!en;
  !!en;


*Handle right clicks in the main arena dialouge and show additional information on right click
!?DL&v998=379/v1000=14;                 [right click on the challenges to show details about the selected arena]

  !!VRz9:S^^;                           [Set to blank text first]
  !!SN:W^Arena_Pages^/?y99;             [Get number of page]
  !!VRz9|v999<20/v999>34:S^^;           [Set to no text if not clicked on levels]
  !!VRy50&y99=1:Sv999-19;               [make correrct offset for arena number]
  !!VRy50&y99=2:Sv999-4;                [make correrct offset for arena number]
  !!VRy50&y99=3:Sv999+11;               [make correrct offset for arena number]
  !!FU(Set_Arena_Descriptions_right_click):Py50; [pass challenge number]
  !!VRz9:Ss^Arena_%Y50_Description_right_click^; [Set text that will show in the description field]

  !!UN:C5203412/2/4/d100;               [increase window length]
  !!UN:C5203503/1/4/d100;
  !!IF&v999>=20/v999<=34:M0/4/^%Z9^;    [Show text on right-click]
  !!UN:C5203412/2/4/d-100;
  !!UN:C5203503/1/4/d-100;              [reduc window length]

!?DL&v998=379/v999=5/v1000=14;          [When clicking on the close Button close DL]

  !!DL381:N^arena4.txt^;                [parse dialogue]
  !!VRz1:S^Rules of the Arena Battle:

  -You cannot die or lose creatures during an arena battle. 
  -So no need to savescum. Everything will be restored in case of win or lose.
  -You can only visit once per week.
  -Visiting arenas costs a little money to compensate the arena master for his trouble.
  -Visting arenas costs 200 movement points.
  -Certain arenas have a scaling with game time.
  -You can get additional information when rmc the arena.
  -rmc the boss to see his stats.^;                           [Set text]
 
  !!DL381:A2/3/z1/1;
  !!FU(DL_ShowPopup):P381;              [Show Popup DL]
   

!?DL&v998=379/v999=30721/v1000=13;      [When clicking on the close Button close DL]
  !!SN:W^Arena_Challenge_Number^/0;     [reset the current arena selection]
  !!SN:W^Arena_Hero^/?y10;              [Get hero who is visiting the Arena]
  !!HEy10:P?y1/?y2/?y3;                 [Get current hero position]
  !!OBy1/y2/y3:T?y4 U?y5;               [Get Type of Object]
  !!POy1/y2/y3&y4=4/y5=0:N1;            [set arena to visited of player pushed exit button]
  !!DL:C1;


!?DL&v998=379/v999=1/v1000=13;                   [When clicking on the close Button close DL]
  !!VRy1:S0R5;
  !!VRz1&y1=0:S^HM334a.wav^;
  !!VRz1&y1=1:S^HM333e.wav^;
  !!VRz1&y1=2:S^HM333d.wav^;
  !!VRz1&y1=3:S^HM332d.wav^;
  !!VRz1&y1=4:S^HM332a.wav^;
  !!VRz1&y1=5:S^HM338c.wav^;  
  !!SN:Pz1;                             [play laugh sound]

****************************************************************************************************

!?FU(Mod_OnExperienceCount)&i^Arena_Mod_Active^;
*If option in arena template is enabled, sets exp gain from this battle to zero

  !!SN:W^Arena_Challenge_Number^/?y10;  [get current arena]
  !!VRy20:Si^Arena_%Y10_HeroGainsNo_Exp^;[check if No Exp gain option is enabled]

  !!if&y20=1;                           [if yes, disable to give exp]
    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/0;
    !!SN:X?y99/0;
    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4628512;
  !!en;

*Create Hook when hero receives experience from battle to set the value to zero
Set hooks at Map Start/Load
!?FU(OnStartOrLoad)&i^Arena_Mod_Active^;
  !!SN:L^EraPlugins\erm_hooker.era^/?y1;
  !!if&y1<>0;
    !!SN:Ay1/^SetHook^/?y2 Ey2/1/4628306/(Mod_OnExperienceCount); [Set Hook provided by Daemon :*]
  !!en;


!?FU(OnStartOrLoad);
!!SN:L^erm_hooker.era^/?(hooker:y);
!!FU&(hooker)=0:E;   

!!SN:A(hooker)/^SetHook^/?(value:y);
!!UN:C4603972/1/235;
!!SN:E(value)/1/4601891/(Arena_SetNewBattlefields);


!?FU(Arena_Get_Army_HP);
*stores army HP value in variable
!!SN:W^Arena_Total_Health^/0;
!!re i/0/21;
  !!BMi:N?y1 H?y2;                      [Get number of units and HP]
  !!VRy3:Sy1*y2;                        [multiply number and HP]
  !!SN:W^Arena_Total_Health^/dy3;       [add health to variable]
!!en;


!?FU(Arena_mod_calc_scaling);
x1=hero x2=returns value
  !!HEx1:F?y90/?y91/?y92/?y93 E?y94/?y95/1; [Get primary stats and hero level from attacking hero]
  !!VRy85:Si^Arena_%X3_Guard_Number_Hero_LvL^; !!VRy85:*y95;
  !!VRy86:Si^Arena_%X3_Guard_Number_Hero_Att^; !!VRy86:*y90;
  !!VRy87:Si^Arena_%X3_Guard_Number_Hero_Def^; !!VRy87:*y91;
  !!VRy88:Si^Arena_%X3_Guard_Number_Hero_Spe^; !!VRy88:*y92;
  !!VRy89:Si^Arena_%X3_Guard_Number_Hero_Kno^; !!VRy89:*y93;
  !!VRy80:Si^Arena_%X3_Guard_Number_HP_based^; [Check if the HP option is activated]
  !!VRx2:Sy80+y85+y86+y87+y88+y89;     [Make percentage based and respect all modifiers]
*!IF:M^%X1 %X2 %X3  y85 is%Y85 and y95 is%Y95^;

!?FU(OnGameEnter)&i^Arena_Mod_Active^;
!!SN:W^Arena_Challenge_Number^/0;       [reset variable when DL is created]



****************************************************************************************************
*Unified Arena Template
!?FU(Arena_Challenge);
x1=Arena Number, x2=hero ID

!!FU&x1=0:E;                            [Exit if no selected Arena]
!!VRi^Arena_Fight^:S1;                  [Enable Arena fight variable]

*Handle what happens when the object is visited*
!!VRy30:Si^Arena_%X1_Battlefield_Type^;
!!UN&y30>=0:P68/0;                      [(Temporaily Deactivate New Battlefield mod, so that custom BF can be showed)]
*!VRz10:Ss^Arena_%X1_CB_Name^;
*!VRz12:Ss^Arena_%X1_CB_Greeting_Text^;
  *!IF&1000:Q1^{%Z10}

  %Z12

  %Z11^;                                (Show message when you visit creature bank)
!!SN:W^Surpress_Battle_Commander^/1;    [Battle Commander will not become active in this fight]
!!VRy5:Si^Arena_%X1_Guards_Type_Slot_0^;
!!HEx2:T0/0/0/y5/0;                     [(Trigger fight, fight monsters)]


!?FU(Arena_SetNewBattlefields)&i^Arena_Fight^;
!!SN:W^Arena_Challenge_Number^/?y1;
!!FU(Arena_Mod_OnBeforeBattleUniversal)&y1>0:Py1; [Pass Arena Challenge Number]

!?FU(Arena_Mod_OnBeforeBattleUniversal);[(Before any Battle)]
x1=Arena Number
!!VRy30:Si^Arena_%X1_Battlefield_Type^; [check set battlefield type number]
!!SN:F^PcxPngExists^/^dw%Y30.pcx^;      [check PNG exists]
!!BA&v1:B^dw%Y30.pcx^;                  [Location of background pictures for type 17 dwellings: ...\Mods\[mod name]\Data\pcx\dw[dwelling id].png]
!!BA:Q0;                                [set no auto]


!?FU(OnAfterBattleUniversal)&i^Arena_Mod_Active^;           [(After any Battle)]
!!SN:W^Arena_Challenge_Number^/?y1;
!!BA:O?y20/?y45;                        [Check Owner before Battle]
!!SN:W^Arena_%Y1_Attacker_Owner_Special^/y20; [Save Owner for later]


*Handle CB Rewards here*
!?FU(Arena_Mod_Reward);
x1=Arena Number, x2=hero ID

!!VRy10:Si^Arena_Fight^;                [Check if Arena fight is enabled]

!!VRi^Arena_Fight^:S0;                  [Reset Flag for the Fight so that it does not happen again]
!!FU|x1=0/x2<0/y10=0:E;                 [Exit if it is not Arena Fight]

!!SN:W^Arena_Challenge_Number^/0;       [Reset challenge number]

!!HEx2:O?y50 B2/?y51;                   [(Get Hero Owner and class]
!!VRy51::2;                             [Get from which town the hero is]
!!if&y50=-1;                            [If no Owner]
  !!FU:E;                               [Exit because the fight was lost]
!!en;

!!SN:W^Arena_Attacker_Owner^/?y70;      [Get Owner for later]
!!VRy50:Si^Arena_%X1_Attacker_Owner^;
!!VRy20:Si^Arena_%X1_Attacker_Owner_Special^;
!!FU&y20<>y50:E;                        [Exit if not the same owner meaning fight was lost]

!!SN:W^Arena_%X1_Hero%X2_wins^/d1;      [add one to hero wins]
!!SN:W^Arena_%X1_Player%Y50_wins^/d1;   [add one to player wins]
!!SN:W^Hero_%X2_Arenas_taken^/d1;       [add one to arena counter]

!!VRy40:Si^Arena_%X1_Battlefield_Type^;
!!UN&y40>=0:P68/1;                      (Activate New Battlefield mod again)

!!VRy4:Si^Arena_%X1_Reward_Creatures_Type^;
!!VRy9:Si^Arena_%X1_Reward_Creatures_Number^;
!!VRy5:Si^Arena_%X1_Reward_Experience^;
!!VRy6:Si^Arena_%X1_Reward_1_Artifacts_Class^;
!!VRy11:Si^Arena_%X1_Reward_1_Artifacts_Specific^;
!!VRy12:Si^Arena_%X1_Reward_PrimSkill^; !!VRy12&y12=4:S0R3; (Generate Random Primary Skill rewards)
!!VRy16:Si^Arena_%X1_Reward_PrimSkill_Amount^;

!!VRy40:Si^Arena_%X1_Reward_Wood^;
!!VRy41:Si^Arena_%X1_Reward_Mercury^;
!!VRy42:Si^Arena_%X1_Reward_Ore^;
!!VRy43:Si^Arena_%X1_Reward_Sulfur^;
!!VRy44:Si^Arena_%X1_Reward_Crystal^;
!!VRy45:Si^Arena_%X1_Reward_Gems^;
!!VRy46:Si^Arena_%X1_Reward_Gold^;
!!VRy47:Si^Arena_%X1_Reward_Mithril^;

!!VRy48:Si^Arena_%X1_Reward_Spell_Points^;
!!VRy49:Si^Arena_%X1_Reward_Spell^;

!!IF&y40=-1/y41=-1/y42=-1/y43=-1/y44=-1/y45=-1/y46=-1/y47=-1/y4=-1/y5=-1/y6=-2/y11=-1/y12=-1/y48=-1/y49=-1/1000:M^Unfortunately the creature bank seems to be empty.^;
!!FU&y40=-1/y41=-1/y42=-1/y43=-1/y44=-1/y45=-1/y46=-1/y47=-1/y4=-1/y5=-1/y6=-2/y11=-1/y12=-1/y48=-1/y49=-1:E;

!!VRy21:Si^Arena_%X1_Reward_Scales_with_size^;
!!if&y21=1:;
  !!VRy20:Si^Arena_%X1_Size_Flag_%V998_%V999_%V1000^;
  !!if&y20>1:;                          (Only run if creature bank size was increased)
    !!VRy20&y20=1:S10; !!VRy20&y20=2:S15; !!VRy20&y20=3:S20; !!VRy20&y20=4:S30;
    !!VRy40&y40>0:*y20:10;              (Increase Wood Reward)
    !!VRy41&y41>0:*y20:10;              (Increase Mercury Reward)
    !!VRy42&y42>0:*y20:10;              (Increase Ore Reward)
    !!VRy43&y43>0:*y20:10;              (Increase Sulfur Reward)
    !!VRy44&y44>0:*y20:10;              (Increase Crystal Reward)
    !!VRy45&y45>0:*y20:10;              (Increase Gems Reward)
    !!VRy46&y46>0:*y20:10;              (Increase Gold Reward)
    !!VRy47&y47>0:*y20:10;              (Increase Mithril Reward)
    !!VRy9&y9>0:*y20:10;                (Increase Creature Reward)
    !!VRy5&y5>0:*y20:10;                (Increase Experience Reward)
    !!VRy16&y16>0/y20=30:+1;            (Increase Primary Skill Reward)
    !!VRy48&y48>0:*y20:10;              (Increase Spell Points Reward)    
    !!VRy6&y6=4/y20=30:S8;              (At level 4 Increase Artifact Reward from Minor to Major)
    !!VRy6&y6=2/y20=30:S4;              (At level 4 Increase Artifact Reward from Treasure to Minor)
    !!VRy6&y6=2/y20=20:S4;              (At level 3 Increase Artifact Reward from Treasure to Minor)
    2=treasure, 4=minor, 8=major, 16=relic
  !!en:;
!!en:;

!!VRy21:Si^Arena_%X1_Reward_Scales^;
!!if&y21>0;
  !!VRy20:Si^timerWeek^;               (Put Current Week in Y20)
  !!VRy21:*y20;                        (Percentage growth times week to get total increase)
  !!VRy21:+100;                        (This will be the value in percent to increase the reward)
  !!VRy40&y40>0:*y21:100;              (Increase Wood Reward)
  !!VRy41&y41>0:*y21:100;              (Increase Mercury Reward)
  !!VRy42&y42>0:*y21:100;              (Increase Ore Reward)
  !!VRy43&y43>0:*y21:100;              (Increase Sulfur Reward)
  !!VRy44&y44>0:*y21:100;              (Increase Crystal Reward)
  !!VRy45&y45>0:*y21:100;              (Increase Gems Reward)
  !!VRy46&y46>0:*y21:100;              (Increase Gold Reward)
  !!VRy47&y47>0:*y21:100;              (Increase Mithril Reward)
  !!VRy9&y9>0:*y21:100;                (Increase Creature Reward)
  !!VRy5&y5>0:*y21:100;                (Increase Experience Reward)
  !!VRy48&y48>0:*y21:100;              (Increase Spell Points Reward)
!!en;

!!SN:W^Advanced_Classes_Mod_Active^/?y90;[Set Mod as active]
!!FU(Arena_ACM_Rewards)&y90=1:Px1/x2;   [Jump to special reward section and pass Arena ID and Hero ID]

!!if&y40>0/y40<=1000000:;                (Wood Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/0/y40/1^Having defeated the %Z1, you discover %Y40 Wood.^;
  !!OW:R-1/0/dy40;
!!en:;
!!if&y41>0/y41<=1000000:;                (Mercury Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/1/y41/1^Having defeated the %Z1, you discover %Y41 Mercury.^;
  !!OW:R-1/1/dy41;
!!en:;
!!if&y42>0/y42<=1000000:;                (Ore Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/2/y42/1^Having defeated the %Z1, you discover %Y42 Ore.^;
  !!OW:R-1/2/dy42;
!!en:;
!!if&y43>0/y43<=1000000:;                (Sulfur Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/3/y43/1^Having defeated the %Z1, you discover %Y43 Sulfur.^;
  !!OW:R-1/3/dy43;
!!en:;
!!if&y44>0/y44<=1000000:;                (Crystal Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/4/y44/1^Having defeated the %Z1, you discover %Y44 Crystal.^;
  !!OW:R-1/4/dy44;
!!en:;
!!if&y45>0/y45<=1000000:;                (Gems Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/5/y45/1^Having defeated the %Z1, you discover %Y45 Gems.^;
  !!OW:R-1/5/dy45;
!!en:;
!!if&y46>0/y46<=1000000:;                (Gold Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/6/y46/1^Having defeated the %Z1, you discover %Y46 Gold.^;
  !!OW:R-1/6/dy46;
!!en:;
!!if&y47>0/y47<=1000000:;                (Mithril Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/7/y47/1^Having defeated the %Z1, you discover %Y47 Mithril.^;
  !!OW:R-1/7/dy47;
!!en:;

!!if&y4>=0/y4<=999/y9>0/y9<=10000:;     (Creature Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!VRy4&y4=1/y9=20:Sy51*14+12;                    [Set to faction based level 7 creature] Perry: just some special condition for arena 13 for me
  *!VRy4:R0/0/13;                       (Generate Creature in the Range from x1 to x2)
  *!VRy80:S0R8*14+y4;                   (possibly for a specific faction)
  *!VRy4:Sy80;
  !!UN&y9=1:N3/2/y4/1;
  !!UN&y9>1:N3/2/y4/2;   
  !!if&1000:; If Human
    !!IF:Q1/21/y4/1^Having defeated the %Z1, you discover %Y9 %Z2.^; 
    !!VRy70:S0;
    !!re i/0/6:;
    !!HEx2:C0/i/?y65/?y66; !!VRy70&y65>=0/y66>0:+1; (loop hero slots and count up to 7)    
    !!en:; 
    !!HEx2&y70=7:C2/y4/y9/1;            (give creatures for Player with choice if army is full)
    !!HEx2&y70<7:C2/y4/y9/0;            (give creatures for AI Player without choice if army is not full)
  !!en:;  

  !!VRy4:Si^Arena_%X1_Reward_1_Creatures_Type^;
  !!VRy9:Si^Arena_%X1_Reward_1_Creatures_Number^;
  !!if&y4>=0/y4<=999/y9>0/y9<=10000:;   (Creature Reward second choice)
    *!VRy4:R0/0/13;                     (Generate Creature in the Range from x1 to x2)
    *!VRy80:S0R8*14+y4;                 (possibly for a specific faction)
    *!VRy4:Sy80;
    !!UN&y9=1:N3/2/y4/1;
    !!UN&y9>1:N3/2/y4/2;        
    !!if&1000:; If Human
      !!IF:Q1/21/y4/1^Having defeated the %Z1, you discover %Y9 %Z2.^;
      !!VRy70:S0;
      !!re i/0/6:;
      !!HEx2:C0/i/?y65/?y66; !!VRy70&y65>=0:+1; (loop hero slots and count up to 7)
    !!en:; 
      !!HEx2&y70=7:C2/y4/y9/1;            (give creatures for Player with choice if army is full)
      !!HEx2&y70<7:C2/y4/y9/0;            (give creatures for AI Player without choice if army is not full)
    !!en:;    
  !!en:;
!!en:;

!!if&y5>0/y5<=9999999:;                (Experience Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/17/y5/1^Having defeated the %Z1, you discover %Y5 Experience.^;
  !!HEx2:Edy5;                          (give experience)
!!en:;

!!if&y6>=-1/y6<=16:;                    (Artifact Reward)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;                       (Get name of defeated Monster)

  !!UN:J6/y6/?y13;                      (get a random artifact)  
  *!VRy13:Si^Arena_%X1_Reward_1_Artifacts_Random^; (Get decided random artifact) Comment: If artifact picture should be shown in the Arena DL, this code holds a predetermind artifact from gamestart. 
  *!FU(Arena_1_Check_Forbidden_Artifacts):Py13/?y13/y6; 
  !!VRy14:Si^Arena_%X1_Reward_1_Artifacts_Specific^; (0-250)  
  !!VRy13&y14>-1:Sy14;                  (If specific artifact was set override random generated artifact)
  *!VRy13:R0/146/155;                   (Generate Artifact in the Range from x1 to x2)
  !!UN:N0/2/y13;                        (Get the name of the Artifact)
  !!IF&1000:Q1/8/y13/1^Having defeated the %Z1, you discover:^;
  !!HEx2:Ay13;                          (Give Artifact to Hero)

  !!VRy6:Si^Arena_%X1_Reward_2_Artifacts_Class^;
  !!if&y6>=-1/y6<=16:;                    (Artifact Reward)
    !!UN:J6/y6/?y13;                      (get a random artifact)
    !!FU(Arena_1_Check_Forbidden_Artifacts):Py13/?y13/y6; 
    !!VRy14:Si^Arena_%X1_Reward_2_Artifacts_Specific^; (0-250)
    !!VRy13&y14>-1:Sy14;                  (If specific artifact was set override random generated artifact)
    *!VRy13:R0/146/155;                   (Generate Artifact in the Range from x1 to x2)
    !!UN:N0/2/y13;                        (Get the name of the Artifact)
    !!IF&1000:Q1/8/y13/1^Having defeated the %Z1, you discover:^;
    !!HEx2:Ay13;
  !!en:;

  !!VRy6:Si^Arena_%X1_Reward_3_Artifacts_Class^;
  !!if&y6>=-1/y6<=16:;                    (Artifact Reward)
    !!UN:J6/y6/?y13;                      (get a random artifact)
    !!FU(Arena_1_Check_Forbidden_Artifacts):Py13/?y13/y6; 
    !!VRy14:Si^Arena_%X1_Reward_3_Artifacts_Specific^; (0-250)
    !!VRy13&y14>-1:Sy14;                  (If specific artifact was set override random generated artifact)
    *!VRy13:R0/146/155;                   (Generate Artifact in the Range from x1 to x2)
    !!UN:N0/2/y13;                        (Get the name of the Artifact)
    !!IF&1000:Q1/8/y13/1^Having defeated the %Z1, you discover:^;
    !!HEx2:Ay13;
  !!en:;

  !!VRy6:Si^Arena_%X1_Reward_4_Artifacts_Class^;
  !!if&y6>=-1/y6<=16:;                    (Artifact Reward)
    !!UN:J6/y6/?y13;                      (get a random artifact)
    !!FU(Arena_1_Check_Forbidden_Artifacts):Py13/?y13/y6;    
    !!VRy14:Si^Arena_%X1_Reward_4_Artifacts_Specific^; (0-250)
    !!VRy13&y14>-1:Sy14;                  (If specific artifact was set override random generated artifact)
    *!VRy13:R0/146/155;                   (Generate Artifact in the Range from x1 to x2)
    !!UN:N0/2/y13;                        (Get the name of the Artifact)
    !!IF&1000:Q1/8/y13/1^Having defeated the %Z1, you discover:^;
    !!HEx2:Ay13;
  !!en:;
!!en:;

!!if&y12>=0/y12<=5/y16>0:;              (Primary Skill Rewards)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!VRy17:S31+y12; Random Reward
  !!VRy17&y12=5:S35; All reward 
  !!IF&1000/y12>=0/y12<=3:Q1/y17/y16/1^Having defeated the %Z1, you discover:^;
  !!IF&1000/y12=5:M^Having defeated the %Z1, you learn +%Y16 to all Primary Skills.^;
  !!HEx2&y17=31:Fdy16/d/d/d;
  !!HEx2&y17=32:Fd/dy16/d/d;
  !!HEx2&y17=33:Fd/d/dy16/d;
  !!HEx2&y17=34:Fd/d/d/dy16;
  !!HEx2&y17=35:Fdy16/dy16/dy16/dy16;
!!en:;

!!if&y48>=0:;                           (Spell Points Rewards)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!IF&1000:Q1/35/y48/1^Having defeated the %Z1, you discover:^;
  !!HEx2:Idy48/1;
!!en:;

!!if&y49>=0/y49<=75:;                   (Spell Rewards)
  !!VRy8:Si^Arena_%X1_Guards_Type_Slot_0^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_1^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_2^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_3^; !!VRy8&y8=-1:Si^Arena_%X1_Guards_Type_Slot_4^;
  !!UN:N3/1/y8/2;
  !!if&y49>=70/y49<=75:; 
    !!re i/0/100:;
      !!VRy1:S0T69;                     (Generate Random Spell ID)
      !!SSy1:L?y2;                      (Check Level of Spell)
      !!if&y49=70:;                     (All Random Spell)
        !!br:;                          (Stop searching)
      !!en:;
      !!if&y49=71/y2=1:;                (First Level Spell)
        !!br:;                          (Stop searching)
      !!en:;
      !!if&y49=72/y2=2:;                (Second Level Spell)
        !!br:;                          (Stop searching)
      !!en:;
      !!if&y49=73/y2=3:;                (Third Level Spell)
        !!br:;                          (Stop searching)
      !!en:;
      !!if&y49=74/y2=4:;                (Fourth Level Spell)
        !!br:;                          (Stop searching)
      !!en:;
       !!if&y49=75/y2=5:;               (Fith Level Spell)
        !!br:;                          (Stop searching)
      !!en:;
    !!en:;    
    !!IF&1000/y1>=0/y1<=69:Q1/9/y1/1^Having defeated the %Z1, you learn:^;
    !!HEx2&y1>=0/y1<=69:My1/1;
  !!en:;
  !!IF&1000/y49>=0/y49<=69:Q1/9/y49/1^Having defeated the %Z1, you learn:^;
  !!HEx2&y49>=0/y49<=69:My49/1;
!!en:;


!?FU(Arena_ACM_Rewards);
x1=Arena ID, x2=Hero ID
!!VRz1:Ss^Arena_%X1_Boss_Name^; [get Arena Boss Name]


[Arena 16]
!!if&x1=16;                             
  !!SN:W^Arena_Reward_%X2_Com_Damage^/d15; [add +15% commander damage]
  !!SN:W^Arena_Reward_%X2_Flat_Melee_Damage^/d100; [add +100 flat damage]

  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+100{~} flat damage with every melee attack!
  {~r}+15%{~} commander damage!^;
!!en;

[Arena 17]
!!if&x1=17;                             
  !!SN:W^Arena_Reward_%X2_Com_Attack^/d15; [add +15% commander attack]
  !!SN:W^Arena_Reward_%X2_Flat_Range_Damage^/d100; [add +100 flat damage]

  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+100{~} flat damage with every range attack!
  {~r}+15%{~} commander attack!^;
!!en;

[Arena 18]
!!if&x1=18;                             
  !!SN:W^Arena_Reward_%X2_Com_Health^/d15; [add +15% commander health]
  !!SN:W^Arena_Reward_%X2_Absorb_Damage^/d50; [add +50 flat damage]

  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+50{~} flat damage reduction!
  {~r}+15%{~} commander health!^;
!!en;

[Arena 19]
!!if&x1=19;                             
  !!SN:W^Arena_Reward_%X2_Com_Spell^/d20;[add +15% commander health]
  !!SN:W^ACM_Reward_Magic_Strength_%X2^/d5; [Add Bonus Magic Strength from ACM rewards]

  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+5{~} Magic Strength
  {~r}+20%{~} commander spell modifier!^;
!!en;

[Arena 20]
!!if&x1=20;                             
  !!SN:W^Arena_Reward_%X2_Com_Passive^/d20;[add +20% commander passive]
  !!SN:W^ACM_Reward_Spell_Crit_Chance_%X2^/d4; [Bonus Crit Chance]
  !!SN:W^ACM_Reward_Spell_Crit_Damage_%X2^/d8; [Bonus Crit Damage]

  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+4%/+8%{~} Magic crit chance/damage
  {~r}+20%{~} commander passive modifier!^;
!!en;

[Arena 21]
!!if&x1=21;                             
  !!SN:W^ACM_Reward_Spell_Damage_%X2^/d5;
  !!SN:W^ACM_Reward_Spell_Upgrade_%X2^/d1; add plus 1 spell upgrade
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+5%{~} Spell damage
  {~r}+1{~} Spell Upgrade!^;
!!en;

[Arena 22]
!!if&x1=22;                             
  !!SN:W^ACM_Reward_Physical_Crit_Chance_%X2^/d3;
  !!SN:W^ACM_Reward_Physical_Crit_Damage_%X2^/d6;
  !!SN:W^Arena_Reward_%X2_Max_Kill_Count^/d10; [increase max kill count by 10]
  !!SN:W^Commander_Kill_Counter_%X2^/d10;[increase current kill count by 10]
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+10{~} commander kill count
  {~r}+6%{~} physical crit damage
  {~r}+3%{~} physical crit chance!^;
!!en;

[Arena 23]
!!if&x1=23;                             
  !!SN:W^ACM_Reward_Spell_Damage_%X2^/d10;
  !!SN:W^Arena_Reward_%X2_VSLiving_Damage^/d100;
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+10%{~} Spell damage
  {~r}+100{~} damage vs living units^;
!!en;

[Arena 24]
!!if&x1=24;                             
  !!SN:W^ACM_Reward_Scouting_Chance_%X2^/d50;
   
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+15%{~} commander speed
  {~r}+%{~} increased scouting chance!^;
!!en;

[Arena 25]
!!if&x1=25;                             
  !!SN:W^Arena_Reward_%X2_Com_Attack^/d5; 
  !!SN:W^Arena_Reward_%X2_Com_Defense^/d5; 
  !!SN:W^Arena_Reward_%X2_Com_Damage^/d5; 
  !!SN:W^Arena_Reward_%X2_Com_Health^/d5;   
  !!SN:W^Arena_Reward_%X2_Com_Speed^/d5; 
  !!SN:W^Arena_Reward_%X2_Com_Spell^/d5;  
  !!SN:W^Arena_Reward_%X2_Com_Passive^/d5; 
  !!SN:W^Arena_Reward_%X2_Com_Aura^/d5; 
  
  !!SN:W^Arena_Reward_%X2_Fire_damage^/d100; 
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+5%{~} to all commander stats
  {~r}+100{~} fire damage to attacks!^;
!!en;

[Arena 26]
!!if&x1=26;                             
  !!SN:W^Arena_Reward_%X2_Luck_Damage^/d15; 
  !!SN:W^Arena_Reward_%X2_Luck_Chance^/d1; 
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+15%{~} Luck strike damage
  {~r}+1{~} Luck crit chance!^;
!!en;

[Arena 27]
!!if&x1=27;                             
  !!SN:W^Arena_Reward_%X2_Magic_Str_Water^/d10; 
  !!SN:W^Arena_Reward_%X2_Spell_dmg_Water^/d20; 
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+20%{~} Water spell damage
  {~r}+10{~} Magic Strength for Water spells!^;
!!en;

[Arena 28]
*!if&x1=27;                             
  *!SN:W^Arena_Reward_%X2_Magic_Str_Water^/d10; 
  *!SN:W^Arena_Reward_%X2_Spell_dmg_Water^/d20; 
  *!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+20%{~} Water spell damage
  {~r}+10{~} Magic Strength for Water spells!^;
*!en;

[Arena 29]
!!if&x1=29;                             
  !!SN:W^Arena_Reward_%X2_Magic_Str_Fire^/d10; 
  !!SN:W^Arena_Reward_%X2_Spell_dmg_Fire^/d15;
  !!SN:W^Commander_Kill_Counter_%X2^/d10;[increase current kill count by 10] 
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+10{~} commander kill count
  {~r}+15%{~} Fire spell damage
  {~r}+10{~} Magic Strength for Fire spells!^;
!!en;

[Arena 30]
!!if&x1=30;                             
  !!SN:W^Commander_Kill_Counter_%X2^/d10;[increase current kill count by 10] 
  !!IF&1000:M^Having defeated the {~r}%Z1{~}, you get:

  {~r}+10%{~} HP bonus
  {~r}+1{~} combat speed
  {~r}+10{~} commander kill count^;
!!en;


!?FU(OnCombatRound)&v997=0; !!FU:E;
!!re i/0/20;                            [loop attacker stacks]
  !!BMi:T?y1 N?y2; !!FU&y2=0:E;         [get unit type and number]
  !!FU|y1=145/y1=146/y1=147/y1=148/y1=149:E; [Execlude Warmachines]
  !!BMi:H?y1;                           [get current HP]
  !!VRy1:*110:100;                      [increase value]
  !!BMi:Hy1 S1;                         [increase HP and speed]
!!en;


!?FU(OnSetupBattlefield);
!!SN:W^Arena_Challenge_Number^/?y1;
!!FU(Arena_Mod_OnSetupBattlefield):Py1; [Pass Arena Challenge Number]


!?FU(Arena_Mod_OnSetupBattlefield);
*Handle Creature Position Here*

!!VRy10:Si^Arena_Fight^; 
!!FU&y10=0:E;                           (Exit if it is not Creature Bank Fight)

!!BA:O?y20/?y21;                        (Check Owner before Battle)
!!VRi^Arena_%X1_Attacker_Owner^:Sy20;   (Save Hero owner)     

!!HE-10:R4/0;                           (remove attackers tactics)
!!BF:C;                                 (Clear Battlefield from Obstacles)

!!BA:Q?y99;                             (Check for Quick Battle)

!!VRy50:Si^Arena_%X1_LineUp_CB_Style^; If this option is enabled your creatures will be in CB lineup all in the middle
!!if&y50=1;
  !!BM0:N?y1 T?y2;
  !!BM0&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P57; (Rearange Position from own creatures)
  !!BM1:N?y1 T?y2;
  !!BM1&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P61;
  !!BM2:N?y1 T?y2;
  !!BM2&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P90;
  !!BM3:N?y1 T?y2;
  !!BM3&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P93;
  !!BM4:N?y1 T?y2;
  !!BM4&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P96;
  !!BM5:N?y1 T?y2;
  !!BM5&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P125;
  !!BM6:N?y1 T?y2;
  !!BM6&y1>0/y2<>145/y2<>146/y2<>147/y2<>148:P129;
!!en;

!!VRy80:Si^Arena_%X1_Guard_Number_HP_based^; [Check if the HP option is activated]
!!if&y80>0;
  !!FU(Arena_Get_Army_HP):P;            [Calc total army hp]
  !!SN:W^Arena_Total_Health^/?y44;      [get the value from variable]

  !!HE-10:F?y90/?y91/?y92/?y93 E?y94/?y95/1; [Get primary stats and hero level from attacking hero]
  !!VRy85:Si^Arena_%X1_Guard_Number_Hero_LvL^; !!VRy85:*y95;
  !!VRy86:Si^Arena_%X1_Guard_Number_Hero_Att^; !!VRy86:*y90;
  !!VRy87:Si^Arena_%X1_Guard_Number_Hero_Def^; !!VRy87:*y91;
  !!VRy88:Si^Arena_%X1_Guard_Number_Hero_Spe^; !!VRy88:*y92;
  !!VRy89:Si^Arena_%X1_Guard_Number_Hero_Kno^; !!VRy89:*y93;

  !!VRy81:Sy80+y85+y86+y87+y88+y89;     [Make percentage based and respect all modifiers]
  !!VRy44&y81>0:*y81:100;               [calculate new HP value]
  !!VRy70:Si^Arena_%X1_Guards_Type_Slot_0^;[get the HP of the first slot guard]
  !!MA&y70>=0:Py70/?y71;                [get health of creature type]
  !!VRy45:Sy44:y71;                     [calc new number of guards]
  !!VRy46:S0;
  !!re i/0/7;                           [get total used slots]
    !!VRy1:Si^Arena_%X1_Guards_Type_Slot_%i^;
    !!VRy46&y1>=0:+1;                   [Increase counter by one to get the number of set slots]
  !!en;
  !!VRy47&y46>0:Sy45:y46;               [Devide the total number with the number of set slots]
!!en;


!!VRy1:Si^Arena_%X1_Guards_Type_Slot_0^;      
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_0^; !!VRy2&y80>0/y47>0:Sy47;   
!!BU&y1>=0/y2>0:Sy1/y2/15/1/0/0;        (Summon First creature number and type at position)

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_1^;      
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_1^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0:Sy1/y2/185/1/1/0;       (Summon second guards)

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_2^;     
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_2^; !!VRy2&y80>0/y47>0:Sy47;   
!!BU&y1>=0/y2>0/y99=0/y50=1:Sy1/y2/172/1/2/0;
!!BU&y1>=0/y2>0/y99=1:Sy1/y2/174/1/2/0; (Fix for Quick Combat because he cant summon there, idk why)
!!BU&y1>=0/y2>0/y99=0/y50=0:Sy1/y2/49/1/2/0;

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_3^;     
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_3^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0/y99=0/y50=1:Sy1/y2/2/1/3/0;
!!BU&y1>=0/y2>0/y99=1:Sy1/y2/12/1/3/0;  (Fix for Quick Combat because he cant summon there, idk why)
!!BU&y1>=0/y2>0/y99=0/y50=0:Sy1/y2/151/1/3/0;

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_4^;      
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_4^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0:Sy1/y2/100/1/4/0;

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_5^;      
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_5^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0/y99=0/y50=1:Sy1/y2/87/1/5/0;
!!BU&y1>=0/y2>0/y99=1:Sy1/y2/133/1/5/0; (Fix for Quick Combat because he cant summon there, idk why)
!!BU&y1>=0/y2>0/y99=0/y50=0:Sy1/y2/83/1/5/0;

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_6^;     
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_6^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0/y50=1:Sy1/y2/8/1/6/0;
!!BU&y1>=0/y2>0/y50=0:Sy1/y2/117/1/6/0;

!!VRy1:Si^Arena_%X1_Guards_Type_Slot_7^;     
!!VRy2:Si^Arena_%X1_Guards_Number_Slot_7^; !!VRy2&y80>0/y47>0:Sy47;  
!!BU&y1>=0/y2>0/y50=1:Sy1/y2/178/1/7/0;
!!BU&y1>=0/y2>0/y50=0:Sy1/y2/94/1/7/0;

!!VRy1:Si^Arena_%X1_Stack_Experience^;
!!UN:P900/?y2;                          [Check Stack Experience option]
!!FU(Arena_1_Stack_Experience)&y1=1/y2=1:P; [Run function to give Stack Exp to units]


 [Add experience ranks to neutral stacks]
!?FU(Arena_1_Stack_Experience);
!!VRy-1:Sc0;                            [Current day: y-1]
!!VRy-1:-1;                             [Subtract 1 from current day: y-2]
!!UN:J2/?y-2;                           [Player chosen difficulty level: y-2]
!!VRy-3&y-2=0:S14;                      [Easy interval (14): y-3]
!!VRy-3&y-2=1:S7;                       [Normal interval (7): y-3]
!!VRy-3&y-2=2:S5;                       [Hard interval (5): y-3]
!!VRy-3&y-2=3:S4;                       [Expert interval (4): y-3]
!!VRy-3&y-2=4:S3;                       [Impossible interval (3): y-3]
!!VRy-4:Sy-1 :y-3;                      [Base rank for this date and difficulty: y-4]
!!BA:H0/?y-5;                           [Attacking hero number: y-5]
!!HEy-5:E?y-7/?y-6/1;                   [Hero's experience level: y-6 (y7 isn't used but avoids a bug)]
!!VRy-6:+y-2;                           [Hero's level +game difficulty: y-6]
!!DO(Arena_1_Stack_Experience_1)/21/41/1&y-4>0:Py-4/y-6;

 [Add ranks to Neutral Stacks if option enabled]
!?FU(Arena_1_Stack_Experience_1);
 [x1=base rank, x2=hero's level+1]
!!BMx16:T?y1 N?y2;                      [Creature type (y1), number (y2)]
!!FU|y1<0/y2<1:E;                       [Exit if no stack at this number]
!!FU&y1>=174/y1<=191:E;                 [Exit if it's a Commander]
!!MA:Ly1/?y3;                           [Creature level: y3]
!!VRy3::2 +1;                           [Divide by 2 (dropping fractions) and add 1 to get level divisor (1..4): y3]
!!VRy4:Sx1 :y3;                         [Divide base rank by level divisor to get final rank: y4]
!!FU&y4<1:E;                            [Exit if no ranks added]
!!VRy5:Sx16 *-1 -1;                     [Negative stack index: y5]
!!VRy4&y4>x2:Sx2;                       [If rank is higher than hero's level+game difficulty, set it to hero's level+game difficulty]
!!EAy5:Ey4/12/d/d;                      [Set stack to rank y4]


!?FU(OnCombatRound);                    [On combat round]
!!SN:W^Arena_Challenge_Number^/?y1;
!!FU(Arena_Mod_OnCombatRound_1):Py1;    [Pass Arena Challenge Number]
!!FU(Arena_Mod_OnCombatRound_2):Py1;    [Pass Arena Challenge Number]

!?FU(Arena_Mod_OnCombatRound_1);        [On combat round]
!!VRy1:Si^Arena_Fight^;  !!FU&y1=0:E;[(Exit if it is not Creature Bank Fight)]
!!VRy1:Si^Arena_%X1_Battle_Duration^;   [get battle duration]
!!FU&y1=0:E;                            [Combat must be at least one round]
!!if&y1=v997;
  !!if&x1>15/x1<=30;                    [just play sound for boss battle Arenas 16 till 30]
    !!VRz-1:S^Lord_Xeen_Laugh.wav^;
    !!SN:Pz-1;                          [play laugh sound]
  !!en;
  !!IF:M^The win condition has not been fullfilled.

   {~r}You loose!{~}^;

  !!BU:V0;                              [Win the battle immidiatly for defender]
!!en;


!?FU(Arena_Mod_OnCombatRound_2);        [On combat round]
!!VRy1:Si^Arena_Fight^;  !!FU&y1=0:E;   [Exit if it is not Creature Bank Fight]
!!VRy1:Si^Arena_%X1_Enrage_Timer^; !!FU&y1<=0:E; [get battle duration]

!!if&y1<=v997;
  !!BA:Q?j;                             [check for quick battle]
  !!IF&j=0:L^{~r}The creatures getting enraged and have their stats increased!{~}^;

  !!VRy10:S10;                          [Attack]
  !!VRy11:S10;                          [Defense]
  !!VRy12:S150;                         [Health]
  !!VRy13:S150;                         [Damage]
  !!VRy14:S4;                           [Speed]

  !!re i/21/41;
    !!BMi:N?y1 H?y2 A?y20 D?y21 H?y22 U1/?y23 U2/?y24 S?y25; [Get number of units and HP]
    !!if&y1>0/y2>0;
     !!VRy22:*150:100;                  [Increase HP by 50%]
     !!VRy23:*150:100;                  [Increase HP by 50%]
     !!VRy24:*150:100;                  [Increase HP by 50%]
     !!BMi:Ady10 Ddy11 Hy22 U1/y23 U2/y24 Sdy14; [Set new unit parameters]
    !!en;
  !!en;
!!en;


!?MR2;                                  [Magic Resistance Trigger]
!!SN:W^Arena_Challenge_Number^/?y1;
!!FU(Arena_Mod_MR2):Py1;                [Pass Arena Challenge Number]

!?FU(Arena_Mod_MR2);                    
!!VRy1:Si^Arena_Fight^;  !!FU&y1=0:E;[Exit if it is not Creature Bank Fight]
!!VRy10:Si^Arena_%X1_Magic_Immune^;     [get battle duration]
!!MR:N?y1;                              [Get Creature Number]
!!BMy1:I?y2 T?y3;                       [Get Creature Side and Type]
!!FU|y3=145/y3=146/y3=147/y3=148/y3=149:E; [Exit for Warmachines]
!!MR&y2=1:Fy10;                         [Apply decreased resistance chance to that stack temporarily only for defender side]


!?FU(OnGameEnter);                      
!!VRi^Arena_Fight^:S0;                  [Reset CB fight variable after loading a game]
!!re i/1/50;                            [loop all Arenas]
  !!FU(Arena_Mod_gen_random_arti)&i^timerDay^=1:Pi; [only run at the first day of the game]
!!en;


!?PI;
!!FU(Set_Arena_Descriptions):P;         [load settings into the variables at gamestart]


*On Game Start generate a random artifact
!?FU(Arena_Mod_gen_random_arti);

!!VRy6:Si^Arena_%X1_Reward_1_Artifacts_Class^; [check if artifact has been set as reward]
!!if&y6>=-1/y6<=16/y6<>0:;              [(Artifact Reward)]
  !!UN:J6/y6/?y13;                      [(get a random artifact)]
  !!FU(Arena_Check_Forbidden_Artifacts):Py13/?y13/y6;  
  !!VRy14:Si^Arena_%X1_Reward_1_Artifacts_Specific^; [(0-250)]
  !!VRy13&y14>-1:Sy14;                  [(If specific artifact was set override random generated artifact)]
  *!VRy13:R0/146/155;                   (Generate Artifact in the Range from x1 to x2)
  !!UN:N0/2/y13;                        [(Get the name of the Artifact)]
  !!VRi^Arena_%X1_Reward_1_Artifacts_Random^:Sy13; [(Save random artifact for later)]
!!en;


!?FU(Arena_Check_Forbidden_Artifacts);
x1=Random Generated Artifact, x2=Artifact that gets handed to the player, x3=class of artifact that should be generated
*If the artifact reward was selected and certain artifacts should be excluded from the rewardpool you can do that here*
y1=1 means to artifact is not allowed to be generated, the script will try several time to generate and allowed artifact.
If it finds a not banned artifact the loop will end and the ID of the artifact will get returned in x2.

!!re i/0/20:;                           [Try twenty times to find a valid artifact]
  !!UN:J6/x3/?x1;                       [get a random artifact]
  !!VRy1|x1=0/x1=1/x1=2/x1=3/x1=4/x1=5/x1=6:S1; [Exclude Warmachines, Spell Book and Grail]
  !!VRy1|x1=128/x1=129/x1=130/x1=131/x1=132/x1=133/x1=134/x1=135/x1=136/x1=137/x1=138/x1=139/x1=140:S1; [Exclude Artifacts number 128-140 (mostly combinations artifacts)]
  !!VRy1|x1=141/x1=142/x1=143/x1=144/x1=145/x1=146/x1=147/x1=148/x1=149/x1=150/x1=151:S1; [Exclude Artifacts number 141-151 (mostly new WoG artifacts and some commander artifacts)]
  !!VRy1|x1=152/x1=153/x1=154/x1=155/x1=156/x1=157/x1=158/x1=159/x1=160/x1=161/x1=162:S1; [Exclude Artifacts number 152-162 (some more commander artifacts and Warloards banner)]
  !!VRy1|x1=161/x1=162/x1=163/x1=164/x1=165/x1=166/x1=167/x1=168/x1=169/x1=170:S1; [Exclude Artifacts number 163-170 (mostly blank WoG artifacts)]

  !!if&y1=0:;
    !!br:;                              [end loop if condition met]
  !!en:;
!!en:;
!!VRx2:Sx1;                             [Set returned artifact to the same number as the one you are about to check]


!?FU(WOG_EndOfTurn)&i^timerOnce^=1/i^timerDay^>0/i^timerWeekDay^=7/i^timerIsAi^=0;
*Every Week increase the number of the creatures by %value with Exponentail Growth AND OR Linear

!!re r/1/30;                            (loop 30 times to possibly grow creatures in the Arenas)
  !!VRy20:Si^Arena_%r_Custom_Growth^;   (check if option is enabled)
  !!VRy21:Si^Arena_%r_Custom_Growth_Start^;   (check when the growth should start)  
  !!if&x2=7/y20>0/y21<=i^timerWeek^;    (if the exponential growth option is active and it is the last day of the week)
    !!VRy20:+100;                       (make percentage based)
    !!re i/0/7/1;                       (loop through all slots)
      !!VRy1:Si^Arena_%r_Guards_Number_Slot_%i^;
      !!VRy1&y1>0:*y20:100;              (multiply guards by percentag value)
      !!VRi^Arena_%r_Guards_Number_Slot_%i^:Sy1; (set increased guards)
    !!en:; 
  !!en:;

  !!VRy20:Si^Arena_%r_Custom_Growth_Linear^; (check if option is enabled)
  !!VRy21:Si^Arena_%r_Custom_Growth_Start^;   (check when the growth should start)
  !!if&y20>0/y21<=i^timerWeek^;         (if the linear growth option is active and it is the last day of the week)
    !!re i/0/7/1;                       (loop through all slots)
      !!VRy1:Si^Arena_%r_Guards_Number_Slot_%i^;      
      !!VRy1&y1>0:+y20;                  (add number to guards)
      !!VRi^Arena_%r_Guards_Number_Slot_%i^:Sy1; (set increased guards)      
    !!en; 
  !!en;
!!en;


!?FU(Arena_Calc_Guard_Numbers);
*gets the total amount of creatures in this arena
!!VRx2:S0;
*!re r/1/30;                            (loop 30 times to possibly grow creatures in the Arenas)
    !!re i/0/7/1;                       (loop through all slots)
      !!VRy1:Si^Arena_%X1_Guards_Number_Slot_%i^;
      !!VRx2&y1>0:+y1;                       (accumulate guards and add up in x2      
    !!en:;  
*!en;


*When right clicking the arena description field, show creature hire dialogue with changed stats
!?DL&v998=379/v1000=14;                 [right click on the challenges to show details about the selected arena]

!!if&v999=10;
  !!SN:W^Arena_Challenge_Number^/?y50;  [reset the current arena selection]
  !!if&y50>15/y50<=30;                  [so it only works for boss arenas]
    !!VRy10:Si^Arena_%Y50_Guards_Type_Slot_0^;    
    !!FU(Arena_Set_Creature_Stats_DL):Py10/y50/0; [pass creature ID, Arena ID and 0 means set]
    !!FU(arena_ShowCreatureDlg):Py10/1;
    !!FU(Arena_Set_Creature_Stats_DL):Py10/y50/1; [pass creature ID, Arena ID and 1 means restore]
  !!en;
!!en;


*To show the stats of Arena Bosses when right-clicking their picture. It temporarily sets stats for creatures.
!?FU(Arena_Set_Creature_Stats_DL);
x1= creature ID, x2= Arena ID, x3= 0 means set, 1 means restore

!!if&x3=0;                              [Set new parameters]
  !!MA:Ax1/?y30 Dx1/?y31 Nx1/?y32 Mx1/?y33 Ex1/?y33 Px1/?y34 Sx1/?y35; [get stats from current creature]
  !!SN:H^monname^/x1/1/?z10;            [get creature name]
  !!SN:H^monname^/x1/2/?z11;            [get creature description text]

  !!VRi^Arena_Creature_Attack_Scaled^:Sy30;    [Save base stats from creature]
  !!VRi^Arena_Creature_Defense_Scaled^:Sy31;
  !!VRi^Arena_Creature_Shots^:Sy32;
  !!VRi^Arena_Creature_Damage_Scaled^:Sy33;
  !!VRi^Arena_Creature_Health_Scaled^:Sy34;
  !!VRi^Arena_Creature_Speed_Scaled^:Sy35;
  !!VRs^Arena_Creature_Name^:Sz10;
  !!VRs^Arena_Creature_Description^:Sz11;

  !!VRy30:Si^Arena_%X2_Boss_Attack_Scaled^;    [get new attack value defined in the arena]
  !!VRy31:Si^Arena_%X2_Boss_Defense_Scaled^;     
  !!VRy32:Si^Arena_%X2_Boss_Shots^;      
  !!VRy33:Si^Arena_%X2_Boss_Damage_Scaled^;      
  !!VRy34:Si^Arena_%X2_Boss_Health_Scaled^;     
  !!VRy35:Si^Arena_%X2_Boss_Speed_Scaled^;       
  !!VRz10:Ss^Arena_%X2_Boss_Name^;         
  !!VRz11:Ss^Arena_%X2_Boss_Description^;  
  !!MA:Ax1/y30 Dx1/y31 Nx1/y32 Mx1/y33 Ex1/y33 Px1/y34 Sx1/y35;
  *!SN:H^monname^/x1/0/z10;             [set creature name]
  !!SN:H^monname^/x1/1/z10;             [set creature name]
  !!SN:H^monname^/x1/2/z11;             [set creature description text]
!!en;

!!if&x3=1;                              [restore base parameters]
  !!VRy30:Si^Arena_Creature_Attack_Scaled^;     
  !!VRy31:Si^Arena_Creature_Defense_Scaled^;  
  !!VRy32:Si^Arena_Creature_Shots^;     
  !!VRy33:Si^Arena_Creature_Damage_Scaled^;   
  !!VRy34:Si^Arena_Creature_Health_Scaled^;    
  !!VRy35:Si^Arena_Creature_Speed_Scaled^;   
  !!VRz10:Ss^Arena_Creature_Name^;       
  !!VRz11:Ss^Arena_Creature_Description^;
  !!MA:Ax1/y30 Dx1/y31 Nx1/y32 Mx1/y33 Ex1/y33 Px1/y34 Sx1/y35; [restore saved parameters]
  *!SN:H^monname^/x1/0/z10;             [set creature name]
  !!SN:H^monname^/x1/1/z10;             [set creature name]
  !!SN:H^monname^/x1/2/z11;             [set creature description text]
!!en;


*Code to set creature name during combat
!?BA0&i^Arena_Fight^;                   
!!SN:W^Arena_Challenge_Number^/?y50;    [get the current arena selection]
!!if&y50>15/y50<=30;                    [so it only works for boss arenas]
  !!VRy10:Si^Arena_%Y50_Guards_Type_Slot_0^; [get first creature slot]
  !!SN:H^monname^/y10/0/?z10;           [get creature name singular]
  !!SN:H^monname^/y10/1/?z11;           [get creature name plural]
  !!VRs^Arena_Creature_Name_Singular^:Sz10; [save original name for later]
  !!VRs^Arena_Creature_Name_Plural^:Sz11;[save original name for later]
  !!VRz10:Ss^Arena_%Y50_Boss_Name^;     [get new name in z10]
  !!SN:H^monname^/y10/0/z10;            [set singular creature name]
  !!SN:H^monname^/y10/1/z10;            [set plural creature name]
!!en;

*Code to set creature name back to normal after combat
!?BA1&i^Arena_Fight^;                   
!!SN:W^Arena_Challenge_Number^/?y50;    [get the current arena selection]
!!if&y50>15/y50<=30;                    [so it only works for boss arenas]
  !!VRz10:Ss^Arena_Creature_Name_Singular^; [get original name in z10]
  !!VRz11:Ss^Arena_Creature_Name_Plural^;[get original name in z11]
  !!VRy10:Si^Arena_%Y50_Guards_Type_Slot_0^; [get first creature slot]
  !!SN:H^monname^/y10/0/z10;            [set singular creature name]
  !!SN:H^monname^/y10/1/z11;            [set plural creature name]
!!en;

****************************************************************************************************








****************************************************************************************************
Code by Daemon to forcely enable Health Bars for Arena Fights
!!FU:E;
!!SN:L^GameplayEnhancementsPlugin.era^/?(plugin:y);
!!if&(plugin);
  !!SN:A(plugin)/^SetState^/?(func:y) A(plugin)/^GetState^/?(getStateFunc:y);
  !!if&(func);
    *!SN:E(func)/(CALLCONV_CDECL)/(TRUE)/(FALSE);
  !!en;
  !!if&(getStateFunc);
    !!SN:E(getStateFunc)/(CALLCONV_CDECL)/?(isEnabled:y)/?(isKeyHold:y);
  !!en;
!!en;
****************************************************************************************************

*Code to show creature hire dialogue when right clicking* Coded by Daemon
!?FU(arena_ShowCreatureDlg);
!#VA(monId:x) (isPopUp:x);

!!SN:B?z1/?(dlg:y);
!!VR(addButtons:y):S(isPopUp) B X1;
!!SN:E6243760/(CALLCONV_THISCALL)/(dlg:y)/(monId)/-1/-1/(addButtons); Ctor
!!CM:A?(x:y)/?(y:y);
!!UN:C(dlg)/32/4/?(dlgWidth:y) C(dlg)/36/4/?(dlgHeight:y);
!!FU(arena_GetRectCenter):P(x)/(y)/(dlgWidth:y)/(dlgHeight)/?(xPos:y)/?(yPos:y);
!!FU(H3Dlg_Coords):P(dlg)/(xPos:y)/(yPos:y);
!!if&(isPopUp);
  !!FU(H3Dlg_ShowPopup):P(dlg);
!!el;
  !!SN:E6245280/(CALLCONV_THISCALL)/(dlg:y); Show 
!!en;

!!SN:E6244736/(CALLCONV_THISCALL)/(dlg:y); Dtor 

*?FU(OnAdventureMapRightMouseClick);
*!VR(monId:y):R0/0/110;
*!MA:A(mon)
*!FU(arena_ShowCreatureDlg):P(MON_DEMON)/0;

!?FU(arena_GetRectCenter);
!#VA(x:x) (y:x) (width:x) (height:x) (xPos:x) (yPos:x);

!!VR(xPos):S(width) :2 -(x) *-1 F8/(INT_MAX);
!!VR(yPos):S(height) :2 -(y) *-1 F8/(INT_MAX);

!!UN:C(ADDR_GAME_SCREEN_WIDTH)/4/?(gameScreenWidth:y) C(ADDR_GAME_SCREEN_HEIGHT)/4/?(gameScreenHeight:y);

!!VR(dlgRb:y):S(xPos) +(width);
!!if&(dlgRb)>(gameScreenWidth);
  !!VR(xPos):S(gameScreenWidth)-(width);
!!en;
!!VR(dlgBb:y):S(yPos) +(height);
!!if&(dlgBb)>(gameScreenHeight);
  !!VR(yPos):S(gameScreenHeight) -(height);
!!en;
****************************************************************************************************


!?FU(Set_Arena_Difficulty);

!!HEx1:E?y20/?y21/1 F?y22/?y23/?y24/?y25;[Get level and prim stats of Hero]
!!VRy31:Sy21:2;                         [devide hero level by 2]
!!VRy32:Sy22+y23:5;                     [Sum attack and defense and devide by 5]

!!SN:W^Arena_Boss_Difficulty^/?y1; get difficulty

*!IF:M^Now set stats^; Test
!!VRy1:S2;

!!re i/15/30;                           [loop all Boss Arenas]
  *Attack
  !!SN:W^Arena_%i_Boss_Attack^/?y10;
  !!VRy10&y1=1:-10;
  !!VRy10&y1=2:+0;
  !!VRy10&y1=3:+10;
  !!VRy10&y1=4:+20;
  !!VRy10:+y31+y32; Add scaling factor
  !!SN:W^Arena_%i_Boss_Attack_Scaled^/y10;

  *Defense
  !!SN:W^Arena_%i_Boss_Defense^/?y10;
  !!VRy10&y1=1:-10;
  !!VRy10&y1=2:+0;
  !!VRy10&y1=3:+10;
  !!VRy10&y1=4:+20;
  !!VRy10:+y31+y32; Add scaling factor
  !!SN:W^Arena_%i_Boss_Defense_Scaled^/y10;

  *Damage
  !!SN:W^Arena_%i_Boss_Damage^/?y10;
  !!VRy10&y1=1:-2000;
  !!VRy10&y1=2:+0;
  !!VRy10&y1=3:+2000;
  !!VRy10&y1=4:+5000;
  !!SN:W^Arena_%i_Boss_Damage_Scaled^/y10;

  *Health
  !!SN:W^Arena_%i_Boss_Health^/?y10;
  !!VRy10&y1=1:*3:2;
  !!VRy10&y1=2:*2;
  !!VRy10&y1=3:*3;
  !!VRy10&y1=4:*4;
  !!SN:W^Arena_%i_Boss_Health_Scaled^/y10;

  *Speed
  !!SN:W^Arena_%i_Boss_Speed^/?y10;
  !!VRy10&y1=1:-2;
  !!VRy10&y1=2:+0;
  !!VRy10&y1=3:+2;
  !!VRy10&y1=4:+4;
  !!SN:W^Arena_%i_Boss_Speed_Scaled^/y10;
!!en;


!?FU(Set_Arena_Descriptions_right_click);
x1= Arena number

!!VRy20:Si^Arena_%X1_Costs^;
!!VRy21:Si^Arena_%X1_Visits_Player^;
!!VRy22:Si^Arena_%X1_Visits_Hero^;
!!VRy23:Si^Arena_%X1_Level_Req^;
!!VRy24:Si^Arena_%X1_Level_Req_Max^;
!!VRy25:Si^Arena_%X1_Battle_Duration^;
!!VRy26:Si^Arena_%X1_Enrage_Timer^;
!!VRy27:Si^Arena_%X1_Magic_Immune^;
!!VRy28:Si^Arena_%X1_Custom_Growth_Start^;
!!VRy29:Si^Arena_%X1_Custom_Growth_Linear^;      

!!VRy30:Si^Arena_%X1_Guards_Type_Slot_0^;
!!VRy30:+2;
!!VRs^Arena_%X1_Description_right_click^:S^{~r}Arena %X1 {~>cprsmall.def:%Y30}{~}


{~r}Player{~}
Arena cost: %Y20
Visits per player: %Y21

{~r}Hero{~}
Visits per hero: %Y22
Minimal hero level: %Y23
Maximal hero level: %Y24

{~r}Battle{~}
Maximal battle duration: %Y25
Enrage timer: %Y26
Magic resistance: %Y27

{~r}Arena{~}
Scaling starts at week: %Y28
Units added per week: %Y29
^; 

!!VRz9:Ss^Arena_%X1_Description_right_click^;    [Set text that will show in the description field]







